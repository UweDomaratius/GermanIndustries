spritelayout copper_mine_spritelayout_1 {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}
    building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_NE) == 0);
		xoffset:  0;	yoffset:  0;	zoffset:  0;
		xextent: 1;		yextent: 16;	zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_NW) == 0);
		xoffset:  0;	yoffset:  0;	zoffset:  0;
		xextent: 16;	yextent: 1;		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_COPPER_ORE_MINE_SMALL_BUILDINGS+construction_state-1;
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 16;
		always_draw: 0;
		hide_sprite: construction_state == 0 || (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE));
	}
    building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SE) == 0);
		xoffset: 0;		yoffset:  16;	zoffset:  0;
		xextent: 16;	yextent: 1;		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SW) == 0);
		xoffset: 16;	yoffset:  0;	zoffset:  0;
		xextent: 1;		yextent: 16;	zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
}
spritelayout copper_mine_spritelayout_2 {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}
    building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_NE) == 0);
		xoffset:  0;	yoffset:  0;	zoffset:  0;
		xextent: 1;		yextent: 16;	zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_NW) == 0);
		xoffset:  0;	yoffset:  0;	zoffset:  0;
		xextent: 16;	yextent: 1;		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
    building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SE) == 0);
		xoffset: 0;		yoffset:  16;	zoffset:  0;
		xextent: 16;	yextent: 1;		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SW) == 0);
		xoffset: 16;	yoffset:  0;	zoffset:  0;
		xextent: 1;		yextent: 16;	zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
}
spritelayout copper_mine_spritelayout_3_anim {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}
    building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_NE) == 0);
		xoffset:  0;	yoffset:  0;	zoffset:  0;
		xextent: 1;		yextent: 16;	zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_NW) == 0);
		xoffset:  0;	yoffset:  0;	zoffset:  0;
		xextent: 16;	yextent: 1;		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_COPPER_ORE_MINE_HEADGEAR_CONSTRUCTION_1;
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 2;
		yoffset: 3;
		zoffset: 0;
		xextent: 13;
		yextent: 12;
		zextent: 16;
		always_draw: 0;
		hide_sprite: construction_state != 1;
	}
	building {
		sprite: TTD_COPPER_ORE_MINE_HEADGEAR_CONSTRUCTION_2;
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 2;
		yoffset: 3;
		zoffset: 0;
		xextent: 13;
		yextent: 12;
		zextent: 16;
		always_draw: 0;
		hide_sprite: construction_state != 2;
	}
	building {
		sprite: TTD_COPPER_ORE_MINE_ANIMATED_HEADGEAR + ((animation_frame < 33) ? (animation_frame %3) : 0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 2;
		yoffset: 3;
		zoffset: 0;
		xextent: 13;
		yextent: 12;
		zextent: 16;
		always_draw: 0;
		hide_sprite: construction_state < 3 || (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE));
	}
    building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SE) == 0);
		xoffset: 0;		yoffset:  16;	zoffset:  0;
		xextent: 16;	yextent: 1;		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SW) == 0);
		xoffset: 16;	yoffset:  0;	zoffset:  0;
		xextent: 1;		yextent: 16;	zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
}
spritelayout copper_mine_spritelayout_4 {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: (LOAD_TEMP(13));
		always_draw: 1;
	}
    building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_NE) == 0);
		xoffset:  0;	yoffset:  0;	zoffset:  0;
		xextent: 1;		yextent: 16;	zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_NW) == 0);
		xoffset:  0;	yoffset:  0;	zoffset:  0;
		xextent: 16;	yextent: 1;		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_COPPER_ORE_MINE_BIG_BUILDING + construction_state - 1;
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 16;
		always_draw: 0;
		hide_sprite: construction_state == 0 || (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE));
	}
    building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SE) == 0);
		xoffset: 0;		yoffset:  16;	zoffset:  0;
		xextent: 16;	yextent: 1;		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SW) == 0);
		xoffset: 16;	yoffset:  0;	zoffset:  0;
		xextent: 1;		yextent: 16;	zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
}
spritelayout copper_mine_spritelayout_5 {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}
    building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_NE) == 0);
		xoffset:  0;	yoffset:  0;	zoffset:  0;
		xextent: 1;		yextent: 16;	zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_NW) == 0);
		xoffset:  0;	yoffset:  0;	zoffset:  0;
		xextent: 16;	yextent: 1;		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_COPPER_ORE_MINE_WINCH_BUILDING_1;
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 16;
		always_draw: 0;
		hide_sprite: construction_state != 1 || (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE));
	}
	building {
		sprite: TTD_COPPER_ORE_MINE_WINCH_BUILDING_2 + construction_state-2;
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 16;
		always_draw: 0;
		hide_sprite: construction_state < 2 || (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE));
	}
	building {
		sprite: TTD_SMOKE_SPRITE_DARK_SMALL + (animation_frame / 4);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 2;
		zoffset: 38+ animation_frame;
		xextent: 11;
		yextent: 16;
		zextent: 7;
		hide_sprite: construction_state < 3 || animation_frame > 19;
	}
    building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SE) == 0);
		xoffset: 0;		yoffset:  16;	zoffset:  0;
		xextent: 16;	yextent: 1;		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SW) == 0);
		xoffset: 16;	yoffset:  0;	zoffset:  0;
		xextent: 1;		yextent: 16;	zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
}

switch(FEAT_INDUSTRYTILES, SELF, copper_ore_mine_industry_layout_1_graphics_switch, relative_pos) {
	relative_coord(0, 0): copper_mine_spritelayout_1;
	relative_coord(0, 1): copper_mine_spritelayout_1;
	relative_coord(0, 2): copper_mine_spritelayout_1;
	relative_coord(1, 0): copper_mine_spritelayout_5;
	relative_coord(1, 1): copper_mine_spritelayout_3_anim;
	relative_coord(1, 2): copper_mine_spritelayout_4;
	relative_coord(2, 0): copper_mine_spritelayout_1;
	relative_coord(2, 1): copper_mine_spritelayout_1;
	relative_coord(2, 2): copper_mine_spritelayout_2;
	relative_coord(3, 1): copper_mine_spritelayout_1;
	return 0; // a default is needed, but should never be reached, layout definitions are explicit
}

switch (FEAT_INDUSTRYTILES, SELF, copper_ore_mine_switch_graphics,
	[switch_terrain_sprite(),switch_graphics_init_temp_registers()

	]) {
    copper_ore_mine_industry_layout_1_graphics_switch;
}

//////////////////////////////////////////////////////////////////////////////
// location checks
//////////////////////////////////////////////////////////////////////////////
switch (FEAT_INDUSTRYTILES, SELF, copper_ore_mine_switch_tile_location_check2,
	(nearby_tile_height(-relative_x, -relative_y) + 
	(nearby_tile_slope(-relative_x, -relative_y)== SLOPE_FLAT ? 0 : nearby_tile_slope(-relative_x, -relative_y) == bitmask(IS_STEEP_SLOPE) ? 2 : 1)) 
	== 
	(nearby_tile_height(0, 0) + (nearby_tile_slope(0, 0) == SLOPE_FLAT ? 0 : nearby_tile_slope(0, 0) == bitmask(IS_STEEP_SLOPE) ? 2 : 1))
	) {
	1: return CB_RESULT_LOCATION_ALLOW;
	return CB_RESULT_LOCATION_DISALLOW;
}


switch (FEAT_INDUSTRYTILES, SELF, copper_ore_mine_switch_tile_location_check,
	is_funded_or_prospected() || (generation_not_forbidden_by_param() && not_next_to_industry()))
{
	1: copper_ore_mine_switch_tile_location_check2;
	return CB_RESULT_LOCATION_DISALLOW;
}
	
//////////////////////////////////////////////////////////////////////////////
// industry tile and layout definition
//////////////////////////////////////////////////////////////////////////////
item(FEAT_INDUSTRYTILES, copper_ore_mine_tile, INDUSTRY_TILE_ID_COPPER_ORE_MINE) {
	property {
		substitute:         0;
		land_shape_flags:   0;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
		animation_info: [ANIMATION_LOOPING, 71];
		animation_speed: 2;
		animation_triggers: bitmask(ANIM_TRIGGER_INDTILE_CONSTRUCTION_STATE);
	}
	graphics {
		tile_check: copper_ore_mine_switch_tile_location_check;
		anim_next_frame: return ((animation_frame == 80) ? CB_RESULT_STOP_ANIMATION : CB_RESULT_NEXT_FRAME);
				anim_control: 0;
		copper_ore_mine_switch_graphics;
	}
}

tilelayout copper_ore_mine_industry_layout_1_tilelayout {
	0, 0: copper_ore_mine_tile;
	0, 1: copper_ore_mine_tile;
	0, 2: copper_ore_mine_tile;
	1, 0: copper_ore_mine_tile;
	1, 1: copper_ore_mine_tile;
	1, 2: copper_ore_mine_tile;
	2, 0: copper_ore_mine_tile;
	2, 1: copper_ore_mine_tile;
	2, 2: copper_ore_mine_tile;
	3, 1: copper_ore_mine_tile;
}

switch(FEAT_INDUSTRIES, SELF, copper_ore_mine_switch_location_check_industry_distance,
        (is_industry_funded_or_prospected()) || ((water_distance > 16) &&
		(industry_distance(INDUSTRY_ID_COPPER_ORE_MINE) >= 16) && 
		(industry_distance(INDUSTRY_ID_COPPER_SMELTER) >= 32))) 
{ 
	1: return CB_RESULT_LOCATION_ALLOW;
	return CB_RESULT_LOCATION_DISALLOW;
}


switch(FEAT_INDUSTRIES, SELF, copper_ore_mine_func_calc_max_prod, 
	param_economy_model == 0 ? 128 :
	  current_year <= 1859 ? 4 : 
	  current_year <= 1894 ? (current_year-1860)+4 : 
	  current_year <= 1914 ? (current_year-1895)*4+40 : 
	  current_year <= 1923 ? (current_year-1915)+120 : 
	  current_year <= 1954 ? 128 : 
	  max(4,128-2*(current_year-1955))) {
	return;
}

// if nothing has been transported at all, do not change production (industry might just not be serviced)
switch(FEAT_INDUSTRIES, SELF, copper_ore_mine_switch_prod_change, 
	[STORE_TEMP(production_level, TEMP_REGISTER_CURRENT_PRODUCTION_LEVEL),
	 STORE_TEMP(transported_last_month_pct("CORE"), TEMP_REGISTER_TRANSPORTED_LAST_MONTH_PCT_CARGO0),
	 STORE_TEMP(copper_ore_mine_func_calc_max_prod(),TEMP_REGISTER_MAXIMUM_PRIMARY_PRODUCTION),
	 LOAD_PERM(PERM_REGISTER_RESOURCES) != 0])
{
	1: change_production; // use default rules to adapt production
	return CB_RESULT_IND_PROD_CLOSE; // resources are depleted - close
}

switch(FEAT_INDUSTRIES, SELF, copper_ore_mine_switch_prod_change_ext_coke_sulphur, 
	[STORE_TEMP(production_level, TEMP_REGISTER_CURRENT_PRODUCTION_LEVEL),
	 STORE_TEMP(transported_last_month_pct("PORE"), TEMP_REGISTER_TRANSPORTED_LAST_MONTH_PCT_CARGO0),
	 STORE_TEMP(copper_ore_mine_func_calc_max_prod(),TEMP_REGISTER_MAXIMUM_PRIMARY_PRODUCTION),
	 LOAD_PERM(PERM_REGISTER_RESOURCES) != 0])
{
	1: change_production; // use default rules to adapt production
	return CB_RESULT_IND_PROD_CLOSE; // resources are depleted - close
}

// randomize production at build time based on random bits: 96t min, 152t max
switch(FEAT_INDUSTRIES, SELF, copper_ore_mine_switch_prod_change_build,
	[initialize_extractive_resources(),
	 STORE_TEMP(copper_ore_mine_func_calc_max_prod(),TEMP_REGISTER_MAXIMUM_PRIMARY_PRODUCTION)
	])
{
	randomize_initial_production;
}

produce(copper_ore_mine_produce,
	[],
	[CORE: LOAD_TEMP(TEMP_REGISTER_PRODUCTION_AMOUNT);],
	0 // do not run again
)

produce(copper_ore_mine_produce_ext_coke_sulphur,
	[],
	[PORE: LOAD_TEMP(TEMP_REGISTER_PRODUCTION_AMOUNT);],
	0 // do not run again
)

////////////////////////////////////////////////////////////////////////////////
// calculate how much to produce, based on remaining reserves, current production level etc.
////////////////////////////////////////////////////////////////////////////////
switch(FEAT_INDUSTRIES, SELF, copper_ore_mine_switch_produce,
	[init_primary_production()]) {
	copper_ore_mine_produce;
}

switch(FEAT_INDUSTRIES, SELF, copper_ore_mine_switch_produce_ext_coke_sulphur,
	[init_primary_production()]) {
	copper_ore_mine_produce_ext_coke_sulphur;
}

////////////////////////////////////////////////////////////////////////////////
// build the industry window text here
////////////////////////////////////////////////////////////////////////////////
switch(FEAT_INDUSTRIES, SELF, copper_ore_mine_switch_extra_text, 
	[STORE_TEMP(copper_ore_mine_func_calc_max_prod(),TEMP_REGISTER_MAXIMUM_PRIMARY_PRODUCTION),
	 STORE_TEMP(param_primary_production == 0 ? 100 : param_primary_production, TEMP_REGISTER_PARAM_PRODUCTION_SCALE),
	 STORE_TEMP(production_level * LOAD_TEMP(TEMP_REGISTER_PARAM_PRODUCTION_SCALE) / 100, TEMP_REGISTER_PRODUCTION_AMOUNT),
	 STORE_TEMP(production_level, TEMP_REGISTER_CURRENT_PRODUCTION_LEVEL),
	 STORE_TEMP(
		LOAD_TEMP(TEMP_REGISTER_CURRENT_PRODUCTION_LEVEL) < LOAD_TEMP(TEMP_REGISTER_MAXIMUM_PRIMARY_PRODUCTION) ? string(STR_INDUSTRY_PREREQ_INCREASE_PRODUCTION) : string(STR_INDUSTRY_PREREQ_KEEP_PRODUCTION) | 
		string(STR_EMPTY) << 16,
		TEMP_REGISTER_EXTRA_TEXT_ARG0),
	 STORE_TEMP(LOAD_PERM(PERM_REGISTER_RESOURCES) / 1000,TEMP_REGISTER_EXTRA_TEXT_ARG1),
	 STORE_TEMP(LOAD_PERM(PERM_REGISTER_RESOURCES) / (LOAD_TEMP(TEMP_REGISTER_PRODUCTION_AMOUNT)*8*12),TEMP_REGISTER_EXTRA_TEXT_ARG2)
	]) {
	return string(STR_EXTRACTIVE_INDUSTRY_TEXT);
}

switch(FEAT_INDUSTRIES, SELF, copper_ore_mine_switch_extra_text_fund, 
    [STORE_TEMP(string(STR_1800) | string(STR_1930) << 16, TEMP_REGISTER_EXTRA_TEXT_ARG0),
	current_year]) {
	1800..1930: return string(STR_EMPTY);
	return string(STR_INDUSTRY_AVAILABLE_TIMEFRAME);
}

// check creation type (getbits(extra_callback_info2, 0, 8))
switch(FEAT_INDUSTRIES, SELF, copper_ore_mine_switch_check_availability_map_gen, 
	getbits(extra_callback_info2, 0, 8) == IND_CREATION_GENERATION) {
	1: return CB_RESULT_IND_PROBABILITY_FROM_PROPERTY;
	return CB_RESULT_IND_NO_CONSTRUCTION;
}

// year < 1800: no creation, 1800 <= year < 1930: certain probability, 1930 <= year: no creation (take copper ore from harbours instead)
switch(FEAT_INDUSTRIES, SELF, copper_ore_mine_switch_check_availability, current_year) {
	0..1799: return CB_RESULT_IND_NO_CONSTRUCTION;               // no copper ore mines before 1800
	1930..5000000: copper_ore_mine_switch_check_availability_map_gen;  // check mode, if the map is generated with that starting year, copper ore mines should exist
	return CB_RESULT_IND_PROBABILITY_FROM_PROPERTY;
}

if (param_extension_painting_industries && !param_extension_coke_sulphur) {
	item(FEAT_INDUSTRIES, copper_ore_mine, INDUSTRY_ID_COPPER_ORE_MINE) {
		property {
			substitute: 0;
			map_colour: 122;
			life_type: IND_LIFE_TYPE_EXTRACTIVE;
			min_cargo_distr: 1;
			layouts: [copper_ore_mine_industry_layout_1_tilelayout];
			spec_flags: bitmask(IND_FLAG_LONG_CARGO_TYPE_LISTS);
			conflicting_ind_types: [];
			random_sound_effects: [];
			name: TTD_STR_INDUSTRY_NAME_COPPER_ORE_MINE;
			prob_map_gen: 4;
			prob_in_game: 4;
			prospect_chance: 0.75;
			fund_cost_multiplier: 232;
			remove_cost_multiplier: 0;
			remove_cost_multiplier: 0;
			cargo_types: [produce_cargo("CORE",0)];
			nearby_station_name: string(STR_STATION, string(STR_TOWN),string(STR_STATION_COPPER_ORE_MINE));
		}

		graphics {
			construction_probability: copper_ore_mine_switch_check_availability;
			produce_cargo_arrival:    empty_produce; 
			produce_256_ticks:        copper_ore_mine_switch_produce;
			monthly_prod_change:      copper_ore_mine_switch_prod_change;
			random_prod_change:       copper_ore_mine_switch_prod_change;
			build_prod_change:        copper_ore_mine_switch_prod_change_build;
			location_check:           copper_ore_mine_switch_location_check_industry_distance;
			extra_text_fund:          copper_ore_mine_switch_extra_text_fund;
			extra_text_industry:      copper_ore_mine_switch_extra_text;
		}
	}    
}

if (param_extension_painting_industries && param_extension_coke_sulphur) {
	item(FEAT_INDUSTRIES, copper_ore_mine, INDUSTRY_ID_COPPER_ORE_MINE) {
		property {
			substitute: 0;
			map_colour: 122;
			life_type: IND_LIFE_TYPE_EXTRACTIVE;
			min_cargo_distr: 1;
			layouts: [copper_ore_mine_industry_layout_1_tilelayout];
			spec_flags: bitmask(IND_FLAG_LONG_CARGO_TYPE_LISTS);
			conflicting_ind_types: [];
			random_sound_effects: [];
			name: TTD_STR_INDUSTRY_NAME_COPPER_ORE_MINE;
			prob_map_gen: 4;
			prob_in_game: 4;
			prospect_chance: 0.75;
			fund_cost_multiplier: 232;
			remove_cost_multiplier: 0;
			remove_cost_multiplier: 0;
			cargo_types: [produce_cargo("PORE",0)];
			nearby_station_name: string(STR_STATION, string(STR_TOWN),string(STR_STATION_COPPER_ORE_MINE));
		}

		graphics {
			construction_probability: copper_ore_mine_switch_check_availability;
			produce_cargo_arrival:    empty_produce; 
			produce_256_ticks:        copper_ore_mine_switch_produce_ext_coke_sulphur;
			monthly_prod_change:      copper_ore_mine_switch_prod_change_ext_coke_sulphur;
			random_prod_change:       copper_ore_mine_switch_prod_change_ext_coke_sulphur;
			build_prod_change:        copper_ore_mine_switch_prod_change_build;
			location_check:           copper_ore_mine_switch_location_check_industry_distance;
			extra_text_fund:          copper_ore_mine_switch_extra_text_fund;
			extra_text_industry:      copper_ore_mine_switch_extra_text;
		}
	}    
}