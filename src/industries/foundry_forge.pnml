spriteset(smithy_forge_spriteset_1_0) {
	[10, 10, 64, 80, -31, -49, ANIM | NOCROP, "graphics/industries/smithy_forge_1.png"]
}
spriteset(smithy_forge_spriteset_1_0_snow) {
	[10, 10, 64, 80, -31, -49, ANIM | NOCROP, "graphics/industries/smithy_forge_1_snow.png"]
}
spriteset(smithy_forge_spriteset_2_0) {
	[80, 10, 64, 80, -31, -49, ANIM | NOCROP, "graphics/industries/smithy_forge_1.png"]
}
spriteset(smithy_forge_spriteset_2_0_snow) {
	[80, 10, 64, 80, -31, -49, ANIM | NOCROP, "graphics/industries/smithy_forge_1_snow.png"]
}
			
			
spritelayout smithy_forge_spritelayout_1 {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}                    
	building {
		sprite: smithy_forge_spriteset_1_0(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE));
	}
	building {
		sprite: smithy_forge_spriteset_1_0_snow(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE_SNOW));
	}
	building {
		sprite: TTD_SMOKE_SPRITE_DARK_SMALL + (animation_frame / 4);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 1;
		zoffset: 44+ animation_frame;
		xextent: 11;
		yextent: 16;
		zextent: 7;
		hide_sprite: animation_frame > 19;
	}
	building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SE) == 0);
		xoffset: 0;
		yoffset:  16;
		zoffset:  0;
		xextent: 16;
		yextent: 1;
		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SW) == 0);
		xoffset: 16;
		yoffset:  0;
		zoffset:  0;
		xextent: 1;
		yextent: 16;
		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
}
spritelayout smithy_forge_spritelayout_2 {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}
	building {
		sprite: smithy_forge_spriteset_2_0(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE));
	}
	building {
		sprite: smithy_forge_spriteset_2_0_snow(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE_SNOW));
	}
    building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SE) == 0);
		xoffset: 0;
		yoffset:  16;
		zoffset:  0;
		xextent: 16;
		yextent: 1;
		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SW) == 0);
		xoffset: 16;
		yoffset:  0;
		zoffset:  0;
		xextent: 1;
		yextent: 16;
		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
}

//////////////////////////////////////////////////////////////////////////////
// graphics choice switch chain
//////////////////////////////////////////////////////////////////////////////
switch(FEAT_INDUSTRYTILES, SELF, foundry_forge_industry_graphics_switch_layouts, relative_pos) {
    relative_coord(0, 0): smithy_forge_spritelayout_2;
    relative_coord(1, 0): smithy_forge_spritelayout_1;
}

switch(FEAT_INDUSTRYTILES, SELF, foundry_forge_switch_graphics_construction_state, construction_state) {
    0..2: spritelayout_default_construction_states;
    foundry_forge_industry_graphics_switch_layouts; // default
}


switch (FEAT_INDUSTRYTILES, SELF, foundry_forge_switch_graphics,
	[switch_tile_fences(),
	 switch_terrain_sprite(),switch_graphics_init_temp_registers()
]) {
    foundry_forge_switch_graphics_construction_state;
}

//////////////////////////////////////////////////////////////////////////////
// location checks
//////////////////////////////////////////////////////////////////////////////
// This check works as follows: calculate height of the northernmost tile of the industry and add 0, 1 or 2 for flat, slope. steep slope
// Do the same for the current tile and compare the results. If the result is equal, the terrain is effectively flat
// and industry can be placed.
// It just looks weird due to the stacked ternary operators needed here
switch (FEAT_INDUSTRYTILES, SELF, foundry_forge_switch_tile_location_check2,
			(nearby_tile_height(-relative_x, -relative_y) + 
			(nearby_tile_slope(-relative_x, -relative_y)== SLOPE_FLAT ? 0 : nearby_tile_slope(-relative_x, -relative_y) == bitmask(IS_STEEP_SLOPE) ? 2 : 1)) 
			== 
			(nearby_tile_height(0, 0) + (nearby_tile_slope(0, 0) == SLOPE_FLAT ? 0 : nearby_tile_slope(0, 0) == bitmask(IS_STEEP_SLOPE) ? 2 : 1))
			) {
	1: return CB_RESULT_LOCATION_ALLOW;
	return CB_RESULT_LOCATION_DISALLOW;
}

switch (FEAT_INDUSTRYTILES, SELF, foundry_forge_switch_tile_location_check,
	is_funded_or_prospected() || (generation_not_forbidden_by_param() && not_next_to_industry() && not_next_to_buildings()))
{
	1: foundry_forge_switch_tile_location_check2;
	return CB_RESULT_LOCATION_DISALLOW;
}

//////////////////////////////////////////////////////////////////////////////
// industry tile and layout definition
//////////////////////////////////////////////////////////////////////////////
item(FEAT_INDUSTRYTILES, foundry_forge_tile, INDUSTRY_TILE_ID_FOUNDRY_FORGE) {
	property {
		substitute:         0;
		land_shape_flags:   0;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
		animation_info: [ANIMATION_LOOPING, 47];
		animation_speed: 2;
		animation_triggers: bitmask();
	}
	graphics {
		tile_check: foundry_forge_switch_tile_location_check;
		foundry_forge_switch_graphics;
	}
}

tilelayout foundry_forge_tilelayout {
	0, 0: foundry_forge_tile;
	1, 0: foundry_forge_tile;
}

switch(FEAT_INDUSTRIES, SELF, foundry_forge_switch_location_check_industry_distance,
       [STORE_TEMP(0, TEMP_REGISTER_TOWN_MIN_DISTANCE),
	    STORE_TEMP(32, TEMP_REGISTER_TOWN_MAX_DISTANCE),
	    (distance_from_town() && 
	    (industry_distance(INDUSTRY_ID_COKE_OVEN) >= 32) && 
		(industry_distance(INDUSTRY_ID_INTEGRATED_STEEL_MILL) >= 32) && 
		(industry_distance(INDUSTRY_ID_ALUMINIUM_PLANT) >= 32) && 
		//(industry_distance(INDUSTRY_ID_APPLIANCE_FACTORY) >= 32) && 
	    (industry_distance(INDUSTRY_ID_FOUNDRY_FORGE) >= 16))]) {
	   1: return CB_RESULT_LOCATION_ALLOW;
	   return CB_RESULT_LOCATION_DISALLOW;
}

produce(foundry_forge_produce,
	[COKE: LOAD_TEMP(TEMP_REGISTER_CURRENT_PRODUCTION_AMOUNT) > 0 ? max(LOAD_TEMP(TEMP_REGISTER_CURRENT_PRODUCTION_AMOUNT)*50/100,1) : 0;
	 STEL: LOAD_TEMP(TEMP_REGISTER_CURRENT_PRODUCTION_AMOUNT);],
	[ENSP: LOAD_TEMP(TEMP_REGISTER_CURRENT_PRODUCTION_AMOUNT) > 0 ? max(LOAD_TEMP(TEMP_REGISTER_CURRENT_PRODUCTION_AMOUNT)*90/100,1) : 0;],
	0 // do not run again
)

switch(FEAT_INDUSTRIES, SELF, foundry_forge_switch_produce, 
	[STORE_TEMP(min(incoming_cargo_waiting("COKE"), incoming_cargo_waiting("STEL")), TEMP_REGISTER_INCOMING_CARGO_WAITING0),
	 
	 calculate_raw_material_usage(),
	 calculate_total_production(),

	 store_debug_production_data()
	]) {
	foundry_forge_produce;
}

produce(foundry_forge_produce_ext_aluminium,
	[COKE: LOAD_TEMP(TEMP_REGISTER_PRODUCTION_RAW0)+LOAD_TEMP(TEMP_REGISTER_PRODUCTION_RAW2);
	 STEL: LOAD_TEMP(TEMP_REGISTER_PRODUCTION_RAW1);
	 ALUM: LOAD_TEMP(TEMP_REGISTER_PRODUCTION_RAW3);
	 ],
	[ENSP: LOAD_TEMP(TEMP_REGISTER_CURRENT_PRODUCTION_AMOUNT) > 0 ? max(LOAD_TEMP(TEMP_REGISTER_CURRENT_PRODUCTION_AMOUNT)*LOAD_TEMP(TEMP_REGISTER_PARAM_PRODUCTION_SCALE)/100,1) : 0;],
	0 // do not run again
)

switch(FEAT_INDUSTRIES, SELF, foundry_forge_switch_produce_ext_aluminium, 
	[STORE_TEMP(min(incoming_cargo_waiting("COKE"),incoming_cargo_waiting("STEL")), TEMP_REGISTER_INCOMING_CARGO_WAITING0),
	 STORE_TEMP(min(incoming_cargo_waiting("COKE"),incoming_cargo_waiting("STEL")), TEMP_REGISTER_INCOMING_CARGO_WAITING1),
	 STORE_TEMP(min(incoming_cargo_waiting("COKE")-LOAD_TEMP(TEMP_REGISTER_INCOMING_CARGO_WAITING0),incoming_cargo_waiting("ALUM")), TEMP_REGISTER_INCOMING_CARGO_WAITING2),
	 STORE_TEMP(min(incoming_cargo_waiting("COKE")-LOAD_TEMP(TEMP_REGISTER_INCOMING_CARGO_WAITING0),incoming_cargo_waiting("ALUM")), TEMP_REGISTER_INCOMING_CARGO_WAITING3),
	 
	 calculate_raw_material_usage(),
	 calculate_total_production(),	 
	 calculate_number_of_waiting_cargos(),
	 
	 // calculate productivity: 50% with 1 waiting cargo, 90% with 2 waiting cargos
	 // formula: 50 + (num_waiting_cargos-1)*(40 / (number of possible cargos-1))
	 STORE_TEMP(50 + (LOAD_TEMP(TEMP_REGISTER_NUMBER_OF_WAITING_CARGOS)-1) * (40 / (4-1)), TEMP_REGISTER_PARAM_PRODUCTION_SCALE),
	 
	 store_debug_production_data()
	]) {
	foundry_forge_produce_ext_aluminium;
}

switch(FEAT_INDUSTRIES, SELF, foundry_forge_switch_extra_text_fund, 
	[STORE_TEMP(string(STR_1800), TEMP_REGISTER_EXTRA_TEXT_ARG0),
	current_year]) {
	1800..500000: return string(STR_EMPTY);
	return string(STR_INDUSTRY_NOT_AVAILABLE_BEFORE);
}

// year < 1800: no creation, 1800 <= year: certain probability
switch(FEAT_INDUSTRIES, SELF, foundry_forge_switch_check_availability, 
	current_year) {
	0..1799: return CB_RESULT_IND_NO_CONSTRUCTION;
	return CB_RESULT_IND_PROBABILITY_FROM_PROPERTY;
}

switch(FEAT_INDUSTRIES, SELF, foundry_forge_switch_cargo_subtype, getbits(extra_callback_info2, 16, 8)) {
	ALUM: switch_cargo_subtype;
	COKE: switch_cargo_subtype;
	STEL: switch_cargo_subtype;
	return 0x3800 + string(STR_EMPTY);
}

switch(FEAT_INDUSTRIES, SELF, foundry_forge_switch_update_last_served_date,
	[update_last_served_date(),
	 STORE_PERM(this_month_production("ENSP") != 0 || last_month_production("ENSP") != 0 || 
		incoming_cargo_waiting("COKE") != 0 || incoming_cargo_waiting("STEL") != 0 || 
		this_month_transported("ENSP") != 0 || last_month_transported("ENSP") != 0, PERM_REGISTER_LAST_SERVED11)
	])
{
	check_secondary_industry_closure();
}

switch(FEAT_INDUSTRIES, SELF, foundry_forge_switch_update_last_served_date_ext_aluminium,
	[update_last_served_date(),
	 STORE_PERM(this_month_production("ENSP") != 0 || last_month_production("ENSP") != 0 || 
		incoming_cargo_waiting("COKE") != 0 || incoming_cargo_waiting("STEL") != 0 || incoming_cargo_waiting("ALUM") != 0 ||
		this_month_transported("ENSP") != 0 || last_month_transported("ENSP") != 0, PERM_REGISTER_LAST_SERVED11)
	])
{
	check_secondary_industry_closure();
}

if (param_extension_metallurgy && !param_extension_aluminium) {
	item(FEAT_INDUSTRIES, foundry_forge, INDUSTRY_ID_FOUNDRY_FORGE) {
		property {
			substitute: 0;
			map_colour: 42;
			life_type: IND_LIFE_TYPE_PROCESSING;
			min_cargo_distr: 1;
			layouts: [foundry_forge_tilelayout];
			spec_flags: bitmask(IND_FLAG_LONG_CARGO_TYPE_LISTS);
			conflicting_ind_types: [];
			random_sound_effects: [];
			name: string(STR_IND_FOUNDRY_FORGE);
			prob_map_gen: 5;
			prob_in_game: 3;
			fund_cost_multiplier: 190;
			remove_cost_multiplier: 0;
			remove_cost_multiplier: 0;
			cargo_types: [accept_cargo("COKE"),accept_cargo("STEL"),produce_cargo("ENSP",0)];
			nearby_station_name: string(STR_STATION, string(STR_TOWN),string(STR_STATION_FOUNDRY_FORGE));
		}
			
		graphics {
			construction_probability: foundry_forge_switch_check_availability;
			produce_cargo_arrival:    empty_produce; 
			produce_256_ticks:        foundry_forge_switch_produce;
			monthly_prod_change:      foundry_forge_switch_update_last_served_date;
			random_prod_change:       return CB_RESULT_IND_PROD_NO_CHANGE;
			location_check:           foundry_forge_switch_location_check_industry_distance;
			extra_text_fund:          foundry_forge_switch_extra_text_fund;
			cargo_subtype_display:    foundry_forge_switch_cargo_subtype;
			build_prod_change:        initialize_last_served_date;
		}
	}
}

if (param_extension_metallurgy && param_extension_aluminium) {
	item(FEAT_INDUSTRIES, foundry_forge, INDUSTRY_ID_FOUNDRY_FORGE) {
		property {
			substitute: 0;
			map_colour: 42;
			life_type: IND_LIFE_TYPE_PROCESSING;
			min_cargo_distr: 1;
			layouts: [foundry_forge_tilelayout];
			spec_flags: bitmask(IND_FLAG_LONG_CARGO_TYPE_LISTS);
			conflicting_ind_types: [];
			random_sound_effects: [];
			name: string(STR_IND_FOUNDRY_FORGE);
			prob_map_gen: 5;
			prob_in_game: 3;
			fund_cost_multiplier: 190;
			remove_cost_multiplier: 0;
			remove_cost_multiplier: 0;
			cargo_types: [accept_cargo("COKE"),accept_cargo("STEL"),accept_cargo("ALUM"),produce_cargo("ENSP",0)];
			nearby_station_name: string(STR_STATION, string(STR_TOWN),string(STR_STATION_FOUNDRY_FORGE));
		}
			
		graphics {
			construction_probability: foundry_forge_switch_check_availability;
			produce_cargo_arrival:    empty_produce; 
			produce_256_ticks:        foundry_forge_switch_produce_ext_aluminium;
			monthly_prod_change:      foundry_forge_switch_update_last_served_date_ext_aluminium;
			random_prod_change:       return CB_RESULT_IND_PROD_NO_CHANGE;
			location_check:           foundry_forge_switch_location_check_industry_distance;
			extra_text_fund:          foundry_forge_switch_extra_text_fund;
			extra_text_industry:      return string(STR_FOUNDRY_FORGE_ALUM);
			cargo_subtype_display:    foundry_forge_switch_cargo_subtype;
			build_prod_change:        initialize_last_served_date;
		}
	}
}
