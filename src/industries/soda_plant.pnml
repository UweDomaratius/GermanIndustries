spriteset(soda_plant_spriteset_2_0) {
	[10, 10, 64, 114, -31, -83, ANIM | NOCROP, "graphics/industries/solvay_plant_1.png"]
}
spriteset(soda_plant_spriteset_3_0) {
	[80, 10, 64, 114, -31, -83, ANIM | NOCROP, "graphics/industries/solvay_plant_1.png"]
}
spriteset(soda_plant_spriteset_4_0) {
	[150, 10, 64, 114, -31, -83, ANIM | NOCROP, "graphics/industries/solvay_plant_1.png"]
}
spriteset(soda_plant_spriteset_5_0) {
	[220, 10, 64, 114, -31, -83, ANIM | NOCROP, "graphics/industries/solvay_plant_1.png"]
}
spriteset(soda_plant_spriteset_6_0) {
	[290, 10, 64, 114, -31, -83, ANIM | NOCROP, "graphics/industries/solvay_plant_1.png"]
}

spritelayout soda_plant_spritelayout_empty {
	childsprite {
		sprite: default_spriteset_ground_dirty_concrete;
		hide_sprite: 0;
		always_draw: 1;
	}
}
spritelayout soda_plant_spritelayout_boiler {
	childsprite {
		sprite: default_spriteset_ground_dirty_concrete;
		hide_sprite: 0;
		always_draw: 1;
	}
	building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_NE) == 0);
		xoffset:  0;	yoffset:  0;	zoffset:  0;
		xextent: 1;		yextent: 16;	zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_NW) == 0);
		xoffset:  0;	yoffset:  0;	zoffset:  0;
		xextent: 16;	yextent: 1;		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}	
	building {
		sprite: soda_plant_spriteset_2_0(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: (LOAD_TEMP(127));
	}
	building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SE) == 0);
		xoffset: 0;		yoffset:  16;	zoffset:  0;
		xextent: 16;	yextent: 1;		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SW) == 0);
		xoffset: 16;	yoffset:  0;	zoffset:  0;
		xextent: 1;		yextent: 16;	zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
}
spritelayout soda_plant_spritelayout_chimneys {
	childsprite {
		sprite: default_spriteset_ground_dirty_concrete;
		hide_sprite: 0;
		always_draw: 1;
	}
	building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_NE) == 0);
		xoffset:  0;	yoffset:  0;	zoffset:  0;
		xextent: 1;		yextent: 16;	zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_NW) == 0);
		xoffset:  0;	yoffset:  0;	zoffset:  0;
		xextent: 16;	yextent: 1;		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}	
	building {
		sprite: soda_plant_spriteset_3_0(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: (LOAD_TEMP(127));
	}
	building {
		sprite: TTD_SMOKE_SPRITE_WHITE_BIG + ((animation_frame + 0)%8);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 9;
		yoffset: 0;
		zoffset: 87;
		xextent: 15;
		yextent: 7;
		zextent: 7;
		hide_sprite: 0;
	}
	building {
		sprite: TTD_SMOKE_SPRITE_WHITE_BIG + ((animation_frame + 0)%8);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 2;
		yoffset: 6;
		zoffset: 71;
		xextent: 15;
		yextent: 7;
		zextent: 7;
		hide_sprite: 0;
	}
	building {
		sprite: TTD_SMOKE_SPRITE_WHITE_BIG + ((animation_frame + 0)%8);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 5;
		yoffset: 6;
		zoffset: 71;
		xextent: 15;
		yextent: 7;
		zextent: 7;
		hide_sprite: 0;
	}
	building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SE) == 0);
		xoffset: 0;		yoffset:  16;	zoffset:  0;
		xextent: 16;	yextent: 1;		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SW) == 0);
		xoffset: 16;	yoffset:  0;	zoffset:  0;
		xextent: 1;		yextent: 16;	zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
}
spritelayout soda_plant_spritelayout_tanks_group {
	childsprite {
		sprite: default_spriteset_ground_dirty_concrete;
		hide_sprite: 0;
		always_draw: 1;
	}
	building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_NE) == 0);
		xoffset:  0;	yoffset:  0;	zoffset:  0;
		xextent: 1;		yextent: 16;	zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_NW) == 0);
		xoffset:  0;	yoffset:  0;	zoffset:  0;
		xextent: 16;	yextent: 1;		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}	
	building {
		sprite: soda_plant_spriteset_4_0(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: (LOAD_TEMP(127));
	}
	building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SE) == 0);
		xoffset: 0;		yoffset:  16;	zoffset:  0;
		xextent: 16;	yextent: 1;		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SW) == 0);
		xoffset: 16;	yoffset:  0;	zoffset:  0;
		xextent: 1;		yextent: 16;	zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
}
spritelayout soda_plant_spritelayout_silos {
	childsprite {
		sprite: default_spriteset_ground_dirty_concrete;
		hide_sprite: 0;
		always_draw: 1;
	}
	building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_NE) == 0);
		xoffset:  0;	yoffset:  0;	zoffset:  0;
		xextent: 1;		yextent: 16;	zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_NW) == 0);
		xoffset:  0;	yoffset:  0;	zoffset:  0;
		xextent: 16;	yextent: 1;		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}	
	building {
		sprite: soda_plant_spriteset_5_0(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: (LOAD_TEMP(127));
	}
	building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SE) == 0);
		xoffset: 0;		yoffset:  16;	zoffset:  0;
		xextent: 16;	yextent: 1;		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SW) == 0);
		xoffset: 16;	yoffset:  0;	zoffset:  0;
		xextent: 1;		yextent: 16;	zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
}
spritelayout soda_plant_spritelayout_silos_with_office {
	childsprite {
		sprite: default_spriteset_ground_dirty_concrete;
		hide_sprite: 0;
		always_draw: 1;
	}
	building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_NE) == 0);
		xoffset:  0;	yoffset:  0;	zoffset:  0;
		xextent: 1;		yextent: 16;	zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_NW) == 0);
		xoffset:  0;	yoffset:  0;	zoffset:  0;
		xextent: 16;	yextent: 1;		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}	
	building {
		sprite: soda_plant_spriteset_6_0(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: (LOAD_TEMP(127));
	}
	building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SE) == 0);
		xoffset: 0;		yoffset:  16;	zoffset:  0;
		xextent: 16;	yextent: 1;		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SW) == 0);
		xoffset: 16;	yoffset:  0;	zoffset:  0;
		xextent: 1;		yextent: 16;	zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
}

//////////////////////////////////////////////////////////////////////////////
// graphics choice switch chain
//////////////////////////////////////////////////////////////////////////////

switch(FEAT_INDUSTRYTILES, SELF, soda_plant_switch_graphics_layout_1, relative_pos) {
	relative_coord(0, 0): soda_plant_spritelayout_chimneys;
	relative_coord(0, 1): soda_plant_spritelayout_boiler;
	relative_coord(0, 2): soda_plant_spritelayout_tanks_group;
	relative_coord(1, 0): soda_plant_spritelayout_chimneys;
	relative_coord(1, 1): soda_plant_spritelayout_boiler;
	relative_coord(1, 2): soda_plant_spritelayout_tanks_group;
	relative_coord(2, 0): soda_plant_spritelayout_silos_with_office;
	relative_coord(2, 1): soda_plant_spritelayout_silos;
	relative_coord(2, 2): soda_plant_spritelayout_empty;
	return 0; // a default is needed, but should never be reached, layout definitions are explicit
}
switch(FEAT_INDUSTRYTILES, SELF, soda_plant_switch_graphics_layout_2, relative_pos) {
	relative_coord(0, 0): soda_plant_spritelayout_boiler;
	relative_coord(0, 1): soda_plant_spritelayout_tanks_group;
	relative_coord(1, 0): soda_plant_spritelayout_boiler;
	relative_coord(1, 1): soda_plant_spritelayout_tanks_group;
	relative_coord(2, 0): soda_plant_spritelayout_chimneys;
	relative_coord(2, 1): soda_plant_spritelayout_silos;
	relative_coord(3, 0): soda_plant_spritelayout_silos_with_office;
	relative_coord(3, 1): soda_plant_spritelayout_empty;
	return 0; // a default is needed, but should never be reached, layout definitions are explicit
}
switch(FEAT_INDUSTRYTILES, SELF, soda_plant_switch_graphics_layout_3, relative_pos) {
	relative_coord(0, 0): soda_plant_spritelayout_boiler;
	relative_coord(0, 1): soda_plant_spritelayout_tanks_group;
	relative_coord(0, 2): soda_plant_spritelayout_chimneys;
	relative_coord(0, 3): soda_plant_spritelayout_silos;
	relative_coord(1, 0): soda_plant_spritelayout_boiler;
	relative_coord(1, 1): soda_plant_spritelayout_tanks_group;
	relative_coord(1, 2): soda_plant_spritelayout_silos_with_office;
	relative_coord(1, 3): soda_plant_spritelayout_empty;
	return 0; // a default is needed, but should never be reached, layout definitions are explicit
}

switch(FEAT_INDUSTRYTILES, PARENT, soda_plant_switch_graphics_layout, layout_num) {
    1: soda_plant_switch_graphics_layout_1;
    2: soda_plant_switch_graphics_layout_2;
    3: soda_plant_switch_graphics_layout_3;
    soda_plant_switch_graphics_layout_1; // default
}

switch(FEAT_INDUSTRYTILES, SELF, soda_plant_switch_graphics_construction_state, construction_state) {
    0..2: spritelayout_default_construction_states;
    soda_plant_switch_graphics_layout; // default
}

switch (FEAT_INDUSTRYTILES, SELF, soda_plant_switch_graphics,
	[switch_terrain_sprite(),switch_graphics_init_temp_registers()
	]) {
    soda_plant_switch_graphics_construction_state;
}

//////////////////////////////////////////////////////////////////////////////
// location checks
//////////////////////////////////////////////////////////////////////////////
// This check works as follows: calculate height of the northernmost tile of the industry and add 0, 1 or 2 for flat, slope. steep slope
// Do the same for the current tile and compare the results. If the result is equal, the terrain is effectively flat
// and industry can be placed.
// It just looks weird due to the stacked ternary operators needed here
switch (FEAT_INDUSTRYTILES, SELF, soda_plant_switch_tile_location_check2,
			(nearby_tile_height(-relative_x, -relative_y) + 
			(nearby_tile_slope(-relative_x, -relative_y)== SLOPE_FLAT ? 0 : nearby_tile_slope(-relative_x, -relative_y) == bitmask(IS_STEEP_SLOPE) ? 2 : 1)) 
			== 
			(nearby_tile_height(0, 0) + (nearby_tile_slope(0, 0) == SLOPE_FLAT ? 0 : nearby_tile_slope(0, 0) == bitmask(IS_STEEP_SLOPE) ? 2 : 1))
			) {
	1: return CB_RESULT_LOCATION_ALLOW;
	return CB_RESULT_LOCATION_DISALLOW;
}

switch (FEAT_INDUSTRYTILES, SELF, soda_plant_switch_tile_location_check,
	is_funded_or_prospected() || (generation_not_forbidden_by_param() && not_next_to_industry() && not_next_to_buildings()))
{
	1: soda_plant_switch_tile_location_check2;
	return CB_RESULT_LOCATION_DISALLOW;
}

//////////////////////////////////////////////////////////////////////////////
// industry tile and layout definition
//////////////////////////////////////////////////////////////////////////////
item(FEAT_INDUSTRYTILES, soda_plant_tile, INDUSTRY_TILE_ID_SODA_PLANT) {
	property {
		substitute:         0;
		land_shape_flags:   0;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
		animation_info: [ANIMATION_LOOPING, 7];
		animation_speed: 3;
		animation_triggers: bitmask(ANIM_TRIGGER_INDTILE_CONSTRUCTION_STATE);
	}
	graphics {
		tile_check: soda_plant_switch_tile_location_check;
		soda_plant_switch_graphics;
	}
}

tilelayout soda_plant_tilelayout_1 {
	0, 0: soda_plant_tile;
	0, 1: soda_plant_tile;
	0, 2: soda_plant_tile;
	1, 0: soda_plant_tile;
	1, 1: soda_plant_tile;
	1, 2: soda_plant_tile;
	2, 0: soda_plant_tile;
	2, 1: soda_plant_tile;
	2, 2: soda_plant_tile;
}
tilelayout soda_plant_tilelayout_2 {
	0, 0: soda_plant_tile;
	0, 1: soda_plant_tile;
	1, 0: soda_plant_tile;
	1, 1: soda_plant_tile;
	2, 0: soda_plant_tile;
	2, 1: soda_plant_tile;
	3, 0: soda_plant_tile;
	3, 1: soda_plant_tile;
}

tilelayout soda_plant_tilelayout_3 {
	0, 0: soda_plant_tile;
	0, 1: soda_plant_tile;
	0, 2: soda_plant_tile;
	0, 3: soda_plant_tile;
	1, 0: soda_plant_tile;
	1, 1: soda_plant_tile;
	1, 2: soda_plant_tile;
	1, 3: soda_plant_tile;
}

switch(FEAT_INDUSTRIES, SELF, soda_plant_switch_location_check_industry_distance,
	   (industry_distance(INDUSTRY_ID_AMMONIA_PLANT) >= 32) &&
	   (industry_distance(INDUSTRY_ID_GLASS_WORKS) >= 32) &&
	   //(industry_distance(INDUSTRY_ID_CLEANING_AGENTS_PLANT) >= 32) &&
	   (industry_distance(INDUSTRY_ID_SODA_PLANT) >= 16) &&
	   (town_euclidean_dist(0,0) > 32) // should not be near towns
	   ) 
{
	   1: return CB_RESULT_LOCATION_ALLOW;
	   return CB_RESULT_LOCATION_DISALLOW;
}

// about 1.5t salt and 0.8t lime for 1t soda => 1t salt + 0.5t lime -> 0.667t soda
produce(soda_plant_produce,
	[SALT: LOAD_TEMP(TEMP_REGISTER_PRODUCTION_RAW0);
	 LIME: LOAD_TEMP(TEMP_REGISTER_CURRENT_PRODUCTION_AMOUNT) > 0 ? max(LOAD_TEMP(TEMP_REGISTER_PRODUCTION_RAW0)*50/100,1) : 0;
	 NH3_: LOAD_TEMP(TEMP_REGISTER_PRODUCTION_RAW2);
	],
	[SASH: LOAD_TEMP(TEMP_REGISTER_CURRENT_PRODUCTION_AMOUNT) > 0 ? max(LOAD_TEMP(TEMP_REGISTER_CURRENT_PRODUCTION_AMOUNT)*67/100,1) : 0;],
	0 // do not run again
)

switch(FEAT_INDUSTRIES, SELF, soda_plant_switch_produce, 
	[STORE_TEMP(min(incoming_cargo_waiting("SALT"), incoming_cargo_waiting("LIME")), TEMP_REGISTER_INCOMING_CARGO_WAITING0),
	STORE_TEMP(min(incoming_cargo_waiting("SALT"), incoming_cargo_waiting("LIME")), TEMP_REGISTER_INCOMING_CARGO_WAITING1),
	 
	 calculate_raw_material_usage(),
	 calculate_total_production(),
	 
	 STORE_TEMP(50 + (LOAD_TEMP(TEMP_REGISTER_NUMBER_OF_WAITING_CARGOS)-1) * (40 / (2-1)), TEMP_REGISTER_PARAM_PRODUCTION_SCALE),
	 
	 STORE_TEMP(incoming_cargo_waiting("NH3_"), TEMP_REGISTER_INCOMING_CARGO_WAITING2),
	 
	 STORE_TEMP(LOAD_TEMP(TEMP_REGISTER_INCOMING_CARGO_WAITING1) > 0 ? 1 : 0, TEMP_REGISTER_NUMBER_OF_WAITING_CARGOS),
	 STORE_TEMP(LOAD_TEMP(TEMP_REGISTER_NUMBER_OF_WAITING_CARGOS) == 1 ? LOAD_TEMP(TEMP_REGISTER_CURRENT_PRODUCTION_AMOUNT) : min(16 + 8*LOAD_TEMP(TEMP_REGISTER_NUMBER_OF_WAITING_CARGOS),LOAD_TEMP(TEMP_REGISTER_CURRENT_PRODUCTION_AMOUNT)), TEMP_REGISTER_CURRENT_PRODUCTION_AMOUNT),
	 STORE_TEMP(min(LOAD_TEMP(TEMP_REGISTER_INCOMING_CARGO_WAITING1), LOAD_TEMP(TEMP_REGISTER_CURRENT_PRODUCTION_AMOUNT) / 5), TEMP_REGISTER_PRODUCTION_RAW2),

	 store_debug_production_data()
	]) {
	soda_plant_produce;
}

switch(FEAT_INDUSTRIES, SELF, soda_plant_switch_cargo_subtype, getbits(extra_callback_info2, 16, 8)) {
	NH3_: switch_cargo_subtype_white;
	LIME: switch_cargo_subtype;
	SALT: switch_cargo_subtype;
	return 0x3800 + string(STR_EMPTY);
}

switch(FEAT_INDUSTRIES, SELF, soda_plant_switch_extra_text_fund, 
	[STORE_TEMP(string(STR_1800), TEMP_REGISTER_EXTRA_TEXT_ARG0),
	current_year]) {
	1800..500000: return string(STR_EMPTY);
	return string(STR_INDUSTRY_NOT_AVAILABLE_BEFORE);
}

switch(FEAT_INDUSTRIES, SELF, soda_plant_switch_check_availability, 
	current_year) {
	0..1799: return CB_RESULT_IND_NO_CONSTRUCTION;
	return CB_RESULT_IND_PROBABILITY_FROM_PROPERTY;
}

switch(FEAT_INDUSTRIES, SELF, soda_plant_switch_update_last_served_date,
	[update_last_served_date(),
	 STORE_PERM(this_month_production("SASH") != 0 || last_month_production("SASH") != 0 || 
		incoming_cargo_waiting("NH3_") != 0 || incoming_cargo_waiting("LIME") != 0 || incoming_cargo_waiting("SALT") != 0 ||
		this_month_transported("SASH") != 0 || last_month_transported("SASH") != 0, PERM_REGISTER_LAST_SERVED11)
	])
{
	check_secondary_industry_closure();
}


if (param_extension_ammonia) {
	item(FEAT_INDUSTRIES, soda_plant, INDUSTRY_ID_SODA_PLANT) {
		property {
			substitute: 0;
			map_colour: 154;
			life_type: IND_LIFE_TYPE_PROCESSING;
			min_cargo_distr: 1;
			layouts: [soda_plant_tilelayout_1,soda_plant_tilelayout_2,soda_plant_tilelayout_3];
			spec_flags: bitmask(IND_FLAG_LONG_CARGO_TYPE_LISTS);
			conflicting_ind_types: [];
			random_sound_effects: [];
			name: string(STR_IND_SODA_PLANT);
			prob_map_gen: 5;
			prob_in_game: 3;
			fund_cost_multiplier: 170;
			remove_cost_multiplier: 0;
			remove_cost_multiplier: 0;
			cargo_types: [accept_cargo("NH3_"),accept_cargo("LIME"),accept_cargo("SALT"),produce_cargo("SASH",0)];
			nearby_station_name: string(STR_STATION, string(STR_TOWN),string(STR_STATION_SODA_PLANT));
		}
			
		graphics {
			construction_probability: soda_plant_switch_check_availability;
			produce_cargo_arrival:    empty_produce; 
			produce_256_ticks:        soda_plant_switch_produce;
			monthly_prod_change:      soda_plant_switch_update_last_served_date;
			random_prod_change:       return CB_RESULT_IND_PROD_NO_CHANGE;
			location_check:           soda_plant_switch_location_check_industry_distance;
			extra_text_fund:          soda_plant_switch_extra_text_fund;
			extra_text_industry:      return string(STR_INDUSTRY_HAS_OPTIONAL_INPUT_CARGO);
			cargo_subtype_display:    soda_plant_switch_cargo_subtype;
			build_prod_change:        initialize_last_served_date;
		}
	}
}
   