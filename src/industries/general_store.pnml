spriteset(general_store_spriteset_1) {
    [10, 10, 64, 31, -31, 0, ANIM | NOCROP, "graphics/industries/general_store.png"]
}
spriteset(general_store_spriteset_1_snow) {
    [10, 10, 64, 31, -31, 0, ANIM | NOCROP, "graphics/industries/general_store_snow.png"]
}
spriteset(general_store_spriteset_2) {
    [10, 60, 64, 48, -31, -18, ANIM | NOCROP, "graphics/industries/general_store.png"]
}
spriteset(general_store_spriteset_2_snow) {
    [10, 60, 64, 48, -31, -18, ANIM | NOCROP, "graphics/industries/general_store_snow.png"]
}
spritelayout general_store_spritelayout {
	childsprite {
		sprite: default_spriteset_ground_slab;
		hide_sprite: 0;
		always_draw: 1;
	}
    childsprite {
        sprite: general_store_spriteset_1(0);
        recolour_mode: RECOLOUR_REMAP;
        palette: PALETTE_USE_DEFAULT;
        hide_sprite: (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE));
        always_draw: 1;
    }
    childsprite {
        sprite: general_store_spriteset_1_snow(0);
        recolour_mode: RECOLOUR_REMAP;
        palette: PALETTE_USE_DEFAULT;
        hide_sprite: (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE_SNOW));
        always_draw: 1;
    }
    building {
        sprite: general_store_spriteset_2(0);
        recolour_mode: RECOLOUR_REMAP;
        palette: PALETTE_USE_DEFAULT;
        xoffset: 0;
        yoffset: 0;
        zoffset: 0;
        xextent: 16;
        yextent: 16;
        zextent: 32;
        always_draw: 0;
        hide_sprite: (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE));
    }
    building {
        sprite: general_store_spriteset_2_snow(0);
        recolour_mode: RECOLOUR_REMAP;
        palette: PALETTE_USE_DEFAULT;
        xoffset: 0;
        yoffset: 0;
        zoffset: 0;
        xextent: 16;
        yextent: 16;
        zextent: 32;
        always_draw: 0;
        hide_sprite: (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE_SNOW));
    }
}

//////////////////////////////////////////////////////////////////////////////
// graphics choice switch chain
//////////////////////////////////////////////////////////////////////////////

switch(FEAT_INDUSTRYTILES, SELF, general_store_switch_graphics_layouts, relative_pos) {
    relative_coord(0, 0): general_store_spritelayout;
    return 0;
}

switch(FEAT_INDUSTRYTILES, SELF, general_store_switch_graphics_construction_state, construction_state) {
    0..2: spritelayout_default_construction_states;
    general_store_switch_graphics_layouts; // default
}

switch (FEAT_INDUSTRYTILES, SELF, general_store_switch_graphics,
	[switch_tile_fences(),
	 switch_terrain_sprite(),switch_graphics_init_temp_registers()]) {
    general_store_switch_graphics_construction_state;
}

//////////////////////////////////////////////////////////////////////////////
// location checks
//////////////////////////////////////////////////////////////////////////////
switch (FEAT_INDUSTRYTILES, SELF, general_store_switch_tile_location_check,
	is_funded_or_prospected() || (generation_not_forbidden_by_param() && is_next_to_road()))
{
	1: return CB_RESULT_LOCATION_ALLOW;
	return CB_RESULT_LOCATION_DISALLOW;
}

//////////////////////////////////////////////////////////////////////////////
// industry tile and layout definition
//////////////////////////////////////////////////////////////////////////////
item(FEAT_INDUSTRYTILES, general_store_tile, INDUSTRY_TILE_ID_GENERAL_STORE) {
	property {
		substitute:         0;
		land_shape_flags:   0;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
		animation_triggers: bitmask();
	}
	graphics {
		tile_check: general_store_switch_tile_location_check;
		general_store_switch_graphics;
	}
}

tilelayout general_store_tilelayout {
	0, 0: general_store_tile;
}

switch(FEAT_INDUSTRIES, SELF, general_store_switch_location_check_industry_distance,
       [(is_industry_funded_or_prospected()) || (
	    (industry_distance(INDUSTRY_ID_FOOD_PROCESSOR) >= 32) &&
		(industry_distance(INDUSTRY_ID_BREWERY) >= 32) &&
		(industry_distance(INDUSTRY_ID_DAIRY) >= 32) &&
		(industry_distance(INDUSTRY_ID_FLOUR_MILL) >= 32) &&
		(industry_distance(INDUSTRY_ID_STOCKYARD) >= 32) &&
		(industry_distance(INDUSTRY_ID_DEPARTMENT_STORE) >= 16) &&
	    (industry_distance(INDUSTRY_ID_GENERAL_STORE) >= 16))]) {
	   1: return CB_RESULT_LOCATION_ALLOW;
	   return CB_RESULT_LOCATION_DISALLOW;
}

// take all incoming cargo and produce nothing
produce(general_store_produce,
	[FOOD: LOAD_TEMP(TEMP_REGISTER_INCOMING_CARGO_WAITING0) > 12 ? LOAD_TEMP(TEMP_REGISTER_INCOMING_CARGO_WAITING0) - 12 : LOAD_TEMP(TEMP_REGISTER_INCOMING_CARGO_WAITING0) != 0 ? 1 : 0;],
	[],
	0 // do not run again
)
switch(FEAT_INDUSTRIES, SELF, general_store_switch_produce,
	[STORE_TEMP(incoming_cargo_waiting("FOOD"), TEMP_REGISTER_INCOMING_CARGO_WAITING0)]) {
	general_store_produce; 
}

produce(general_store_produce_extension_fruits,
	[FOOD: LOAD_TEMP(TEMP_REGISTER_INCOMING_CARGO_WAITING0) > 12 ? LOAD_TEMP(TEMP_REGISTER_INCOMING_CARGO_WAITING0) - 12 : LOAD_TEMP(TEMP_REGISTER_INCOMING_CARGO_WAITING0) != 0 ? 1 : 0;
	 FRUT: LOAD_TEMP(TEMP_REGISTER_INCOMING_CARGO_WAITING1) > 12 ? LOAD_TEMP(TEMP_REGISTER_INCOMING_CARGO_WAITING1) - 12 : LOAD_TEMP(TEMP_REGISTER_INCOMING_CARGO_WAITING1) != 0 ? 1 : 0;
	],
	[],
	0 // do not run again
)
switch(FEAT_INDUSTRIES, SELF, general_store_switch_produce_extension_fruits,
	[STORE_TEMP(incoming_cargo_waiting("FOOD"), TEMP_REGISTER_INCOMING_CARGO_WAITING0),
	 STORE_TEMP(incoming_cargo_waiting("FRUT"), TEMP_REGISTER_INCOMING_CARGO_WAITING1)
	]) {
	general_store_produce_extension_fruits; 
}

switch(FEAT_INDUSTRIES, SELF, general_store_switch_update_last_served_date,
	[update_last_served_date(),
	 STORE_PERM(incoming_cargo_waiting("FOOD"), PERM_REGISTER_LAST_SERVED11)
	])
{
	check_secondary_industry_closure();
}

switch(FEAT_INDUSTRIES, SELF, general_store_switch_update_last_served_date_ext_fruits,
	[update_last_served_date(),
	 STORE_PERM(incoming_cargo_waiting("FOOD") != 0 || incoming_cargo_waiting("FRUT") != 0, PERM_REGISTER_LAST_SERVED11)
	])
{
	check_secondary_industry_closure();
}

if (!param_extension_fruits) {
	item(FEAT_INDUSTRIES, general_store, INDUSTRY_ID_GENERAL_STORE) {
		property {
			substitute: 0;
			map_colour: 168;
			life_type: IND_LIFE_TYPE_BLACK_HOLE;
			min_cargo_distr: 1;
			layouts: [general_store_tilelayout];
			spec_flags: bitmask(IND_FLAG_LONG_CARGO_TYPE_LISTS,IND_FLAG_ONLY_IN_TOWNS);
			conflicting_ind_types: [];
			random_sound_effects: [];
			name: string(STR_IND_GENERAL_STORE);
			prob_map_gen: 24;
			prob_in_game: 12;
			prospect_chance: 0.75;
			fund_cost_multiplier: 15;
			remove_cost_multiplier: 0;
			remove_cost_multiplier: 0;
			cargo_types: [accept_cargo("FOOD")];
			nearby_station_name: string(STR_STATION, string(STR_TOWN),string(STR_STATION_GENERAL_STORE));
		}
			
		graphics {
			construction_probability: get_construction_probability;
			produce_cargo_arrival:    empty_produce; 
			produce_256_ticks:        general_store_switch_produce;
			monthly_prod_change:      CB_RESULT_IND_PROD_NO_CHANGE;
			random_prod_change:       general_store_switch_update_last_served_date;
			location_check:           general_store_switch_location_check_industry_distance;
			extra_text_fund:          return CB_RESULT_NO_TEXT;
			extra_text_industry:      return CB_RESULT_NO_TEXT;
			cargo_subtype_display:    return CB_RESULT_IND_NO_TEXT_NO_AMOUNT;
			build_prod_change:        initialize_last_served_date;
		}
	}
}
 
if (param_extension_fruits) {
	item(FEAT_INDUSTRIES, general_store, INDUSTRY_ID_GENERAL_STORE) {
		property {
			substitute: 0;
			map_colour: 168;
			life_type: IND_LIFE_TYPE_BLACK_HOLE;
			min_cargo_distr: 1;
			layouts: [general_store_tilelayout];
			spec_flags: bitmask(IND_FLAG_LONG_CARGO_TYPE_LISTS,IND_FLAG_ONLY_IN_TOWNS);
			conflicting_ind_types: [];
			random_sound_effects: [];
			name: string(STR_IND_GENERAL_STORE);
			prob_map_gen: 24;
			prob_in_game: 12;
			prospect_chance: 0.75;
			fund_cost_multiplier: 15;
			remove_cost_multiplier: 0;
			remove_cost_multiplier: 0;
			cargo_types: [accept_cargo("FOOD"),accept_cargo("FRUT")];
			nearby_station_name: string(STR_STATION, string(STR_TOWN),string(STR_STATION_GENERAL_STORE));
		}
			
		graphics {
			construction_probability: get_construction_probability;
			produce_cargo_arrival:    empty_produce; 
			produce_256_ticks:        general_store_switch_produce_extension_fruits;
			monthly_prod_change:      CB_RESULT_IND_PROD_NO_CHANGE;
			random_prod_change:       general_store_switch_update_last_served_date_ext_fruits;
			location_check:           general_store_switch_location_check_industry_distance;
			extra_text_fund:          return CB_RESULT_NO_TEXT;
			extra_text_industry:      return CB_RESULT_NO_TEXT;
			cargo_subtype_display:    return CB_RESULT_IND_NO_TEXT_NO_AMOUNT;
			build_prod_change:        initialize_last_served_date;
		}
	}
} 