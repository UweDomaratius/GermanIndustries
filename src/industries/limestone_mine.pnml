spriteset(limestone_mine_spriteset_animated_headgear) {
	[10, 310, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
	[80, 310, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
	[150, 310, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
}        
spriteset(limestone_mine_spriteset_animated_rail) {
	[10, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
	[80, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
	[150, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
	[220, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
	[290, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
	[360, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
	[430, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
	[500, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
	[500, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
	[570, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
	[570, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
	[640, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
	[640, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
	[710, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
	[780, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
	[850, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
	[920, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
	[990, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
	[1060, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
}        
spriteset(limestone_mine_spriteset_2) {
	[10, 10, 64, 122, -31, -90, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
}        
spriteset(limestone_mine_spriteset_3) {
	[80, 10, 64, 122, -31, -74, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
}        
spriteset(limestone_mine_spriteset_4) {
	[150, 10, 64, 122, -31, -90, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
}        
spriteset(limestone_mine_spriteset_5) {
	[220, 10, 64, 122, -31, -90, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
}        
spriteset(limestone_mine_spriteset_6) {
	[290, 10, 64, 122, -31, -90, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
}        
spriteset(limestone_mine_spriteset_7) {
	[360, 10, 64, 122, -31, -90, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
}        
spriteset(limestone_mine_spriteset_8) {
	[430, 10, 64, 122, -31, -90, ANIM | NOCROP, "graphics/industries/limestone_mine.png"]
}        
       
spritelayout limestone_mine_spritelayout_tile_empty {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}
}
spritelayout limestone_mine_spritelayout_headgear_animated {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}            
	building {
		sprite: limestone_mine_spriteset_animated_headgear(1*(animation_frame % 3));
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE));
	}
}
spritelayout limestone_mine_spritelayout_exit_trestle_animated {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}
	building {
		sprite: limestone_mine_spriteset_animated_rail(1* (animation_frame));
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE));
	}
}
spritelayout limestone_mine_spritelayout_crusher_front_part {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}
	building {
		sprite: limestone_mine_spriteset_2(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE));
	}

}
spritelayout limestone_mine_spritelayout_crusher_rear_part {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: (LOAD_TEMP(13));
		always_draw: 1;
	}                    
	building {
		sprite: limestone_mine_spriteset_3(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE));
	}
}
spritelayout limestone_mine_spritelayout_hut_vents {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}
	building {
		sprite: limestone_mine_spriteset_4(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE));
	}
}
spritelayout limestone_mine_spritelayout_ore_1 {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}
	building {
		sprite: limestone_mine_spriteset_5(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE));
	}
}
spritelayout limestone_mine_spritelayout_ore_2 {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}
	building {
		sprite: limestone_mine_spriteset_6(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE));
	}            
}

spritelayout limestone_mine_spritelayout_winding_house {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}
	building {
		sprite: limestone_mine_spriteset_7(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE));
	}            
}
spritelayout limestone_mine_spritelayout_exit_shed_rear {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}                    
	building {
		sprite: limestone_mine_spriteset_8(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE));
	}
}

switch(FEAT_INDUSTRYTILES, SELF, limestone_mine_industry_layout_1_graphics_switch, relative_pos) {
	relative_coord(0, 1): limestone_mine_spritelayout_headgear_animated;
    relative_coord(0, 2): limestone_mine_spritelayout_winding_house;
    relative_coord(1, 0): limestone_mine_spritelayout_crusher_rear_part;
    relative_coord(1, 1): limestone_mine_spritelayout_exit_shed_rear;
    relative_coord(1, 2): limestone_mine_spritelayout_hut_vents;
    relative_coord(2, 0): limestone_mine_spritelayout_tile_empty;
    relative_coord(2, 1): limestone_mine_spritelayout_exit_trestle_animated;
    relative_coord(2, 2): limestone_mine_spritelayout_tile_empty;
    relative_coord(3, 0): limestone_mine_spritelayout_crusher_front_part;
    relative_coord(3, 1): limestone_mine_spritelayout_ore_1;
    relative_coord(3, 2): limestone_mine_spritelayout_ore_2;
	return 0; // a default is needed, but should never be reached, layout definitions are explicit
}

switch(FEAT_INDUSTRYTILES, SELF, limestone_mine_industry_layout_2_graphics_switch, relative_pos) {
	relative_coord(0, 0): limestone_mine_spritelayout_crusher_rear_part;
    relative_coord(0, 1): limestone_mine_spritelayout_headgear_animated;
    relative_coord(0, 2): limestone_mine_spritelayout_winding_house;
    relative_coord(0, 3): limestone_mine_spritelayout_headgear_animated;
    relative_coord(0, 4): limestone_mine_spritelayout_winding_house;
    relative_coord(1, 0): limestone_mine_spritelayout_tile_empty;
    relative_coord(1, 1): limestone_mine_spritelayout_exit_shed_rear;
    relative_coord(1, 2): limestone_mine_spritelayout_tile_empty;
    relative_coord(1, 3): limestone_mine_spritelayout_exit_shed_rear;
    relative_coord(1, 4): limestone_mine_spritelayout_hut_vents;
    relative_coord(2, 0): limestone_mine_spritelayout_crusher_front_part;
    relative_coord(2, 1): limestone_mine_spritelayout_exit_trestle_animated;
    relative_coord(2, 2): limestone_mine_spritelayout_ore_1;
    relative_coord(2, 3): limestone_mine_spritelayout_exit_trestle_animated;
    relative_coord(2, 4): limestone_mine_spritelayout_ore_2;
	return 0; // a default is needed, but should never be reached, layout definitions are explicit
}

switch(FEAT_INDUSTRYTILES, SELF, limestone_mine_industry_layout_3_graphics_switch, relative_pos) {
	relative_coord(0, 0): limestone_mine_spritelayout_headgear_animated;
    relative_coord(0, 1): limestone_mine_spritelayout_winding_house;
    relative_coord(1, 0): limestone_mine_spritelayout_exit_shed_rear;
    relative_coord(1, 1): limestone_mine_spritelayout_ore_2;
    relative_coord(1, 2): limestone_mine_spritelayout_tile_empty;
    relative_coord(2, 0): limestone_mine_spritelayout_exit_trestle_animated;
    relative_coord(2, 1): limestone_mine_spritelayout_headgear_animated;
    relative_coord(2, 2): limestone_mine_spritelayout_winding_house;
    relative_coord(3, 2): limestone_mine_spritelayout_hut_vents;
    relative_coord(3, 1): limestone_mine_spritelayout_exit_shed_rear;
    relative_coord(3, 0): limestone_mine_spritelayout_crusher_rear_part;
    relative_coord(4, 0): limestone_mine_spritelayout_tile_empty;
    relative_coord(4, 1): limestone_mine_spritelayout_exit_trestle_animated;
    relative_coord(4, 2): limestone_mine_spritelayout_ore_2;
    relative_coord(5, 0): limestone_mine_spritelayout_crusher_front_part;
    relative_coord(5, 1): limestone_mine_spritelayout_ore_1;
	return 0; // a default is needed, but should never be reached, layout definitions are explicit
}

switch(FEAT_INDUSTRYTILES, SELF, limestone_mine_industry_layout_4_graphics_switch, relative_pos) {
	relative_coord(0, 0): limestone_mine_spritelayout_headgear_animated;
    relative_coord(0, 1): limestone_mine_spritelayout_winding_house;
    relative_coord(0, 2): limestone_mine_spritelayout_headgear_animated;
    relative_coord(0, 3): limestone_mine_spritelayout_winding_house;
    relative_coord(0, 4): limestone_mine_spritelayout_crusher_rear_part;
    relative_coord(1, 0): limestone_mine_spritelayout_exit_shed_rear;
    relative_coord(1, 1): limestone_mine_spritelayout_ore_2;
    relative_coord(1, 2): limestone_mine_spritelayout_exit_shed_rear;
    relative_coord(1, 3): limestone_mine_spritelayout_tile_empty;
    relative_coord(1, 4): limestone_mine_spritelayout_tile_empty;
    relative_coord(2, 0): limestone_mine_spritelayout_exit_trestle_animated;
    relative_coord(2, 1): limestone_mine_spritelayout_hut_vents;
    relative_coord(2, 2): limestone_mine_spritelayout_exit_trestle_animated;
    relative_coord(2, 3): limestone_mine_spritelayout_ore_1;
    relative_coord(2, 4): limestone_mine_spritelayout_crusher_front_part;
	return 0; // a default is needed, but should never be reached, layout definitions are explicit
}

switch(FEAT_INDUSTRYTILES, PARENT, limestone_mine_industry_graphics_switch_layouts, layout_num) {
    1: limestone_mine_industry_layout_1_graphics_switch;
    2: limestone_mine_industry_layout_2_graphics_switch;
    3: limestone_mine_industry_layout_3_graphics_switch;
    4: limestone_mine_industry_layout_4_graphics_switch;
    limestone_mine_industry_layout_1_graphics_switch; // default
}

switch(FEAT_INDUSTRYTILES, SELF, limestone_mine_switch_graphics_construction_state, construction_state) {
    0..2: spritelayout_default_construction_states;
    limestone_mine_industry_graphics_switch_layouts; // default
}

switch (FEAT_INDUSTRYTILES, SELF, limestone_mine_switch_graphics,
	[switch_terrain_sprite(),switch_graphics_init_temp_registers()
	]) {
    limestone_mine_switch_graphics_construction_state;
}

//////////////////////////////////////////////////////////////////////////////
// location checks
//////////////////////////////////////////////////////////////////////////////
switch (FEAT_INDUSTRYTILES, SELF, limestone_mine_switch_tile_location_check2,
	(nearby_tile_height(-relative_x, -relative_y) + 
	(nearby_tile_slope(-relative_x, -relative_y)== SLOPE_FLAT ? 0 : nearby_tile_slope(-relative_x, -relative_y) == bitmask(IS_STEEP_SLOPE) ? 2 : 1)) 
	== 
	(nearby_tile_height(0, 0) + (nearby_tile_slope(0, 0) == SLOPE_FLAT ? 0 : nearby_tile_slope(0, 0) == bitmask(IS_STEEP_SLOPE) ? 2 : 1))
	) {
	1: return CB_RESULT_LOCATION_ALLOW;
	return CB_RESULT_LOCATION_DISALLOW;
}


switch (FEAT_INDUSTRYTILES, SELF, limestone_mine_switch_tile_location_check,
	is_funded_or_prospected() || (generation_not_forbidden_by_param() && not_next_to_industry()))
{
	1: limestone_mine_switch_tile_location_check2;
	return CB_RESULT_LOCATION_DISALLOW;
}

switch(FEAT_INDUSTRYTILES, PARENT, limestone_mine_switch_tile_cargo_acceptance, 
	[STORE_TEMP(incoming_cargo_waiting("BOOM") > 60000 ? 0 : 8,TEMP_REGISTER_STOCKPILE_FULL0)
	]
)
{
	return LOAD_TEMP(TEMP_REGISTER_STOCKPILE_FULL0);
}
	
//////////////////////////////////////////////////////////////////////////////
// industry tile and layout definition
//////////////////////////////////////////////////////////////////////////////
item(FEAT_INDUSTRYTILES, limestone_mine_tile_animated_headgear, INDUSTRY_TILE_ID_LIMESTONE_MINE_1) {
	if (!param_extension_production_boost) {
		property {
			substitute:         0;
			land_shape_flags:   0;
			accepted_cargos: [];
			animation_info: [ANIMATION_LOOPING, 81];
			animation_speed: 1;
			animation_triggers: bitmask(ANIM_TRIGGER_INDTILE_TILE_LOOP);
		}
	}
	if (param_extension_production_boost) {
		property {
			substitute:         0;
			land_shape_flags:   0;
			accepted_cargos: [[BOOM,8]];
			animation_info: [ANIMATION_LOOPING, 81];
			animation_speed: 1;
			animation_triggers: bitmask(ANIM_TRIGGER_INDTILE_TILE_LOOP);
		}
	}
	graphics {
		tile_check: limestone_mine_switch_tile_location_check;
		anim_next_frame: return ((animation_frame == 80) ? CB_RESULT_STOP_ANIMATION : CB_RESULT_NEXT_FRAME);
		anim_control: 0;
		cargo_amount_accept: limestone_mine_switch_tile_cargo_acceptance;
		limestone_mine_switch_graphics;
	}
}
item(FEAT_INDUSTRYTILES, limestone_mine_tile_2, INDUSTRY_TILE_ID_LIMESTONE_MINE_2) {
	if (!param_extension_production_boost) {
		property {
			substitute:         0;
			land_shape_flags:   0;
			accepted_cargos: [];
			animation_info: [ANIMATION_LOOPING, 7];
			animation_speed: 3;
			animation_triggers: bitmask(ANIM_TRIGGER_INDTILE_CONSTRUCTION_STATE);
		}
	}
	if (param_extension_production_boost) {
		property {
			substitute:         0;
			land_shape_flags:   0;
			accepted_cargos: [[BOOM,8]];
			animation_info: [ANIMATION_LOOPING, 7];
			animation_speed: 3;
			animation_triggers: bitmask(ANIM_TRIGGER_INDTILE_CONSTRUCTION_STATE);
		}
	}
	graphics {
		tile_check: limestone_mine_switch_tile_location_check;
		anim_control: 0;
		cargo_amount_accept: limestone_mine_switch_tile_cargo_acceptance;
		limestone_mine_switch_graphics;
	}
}
item(FEAT_INDUSTRYTILES, limestone_mine_tile_exit_trestle, INDUSTRY_TILE_ID_LIMESTONE_MINE_3) {
	if (!param_extension_production_boost) {
		property {
			substitute:         0;
			land_shape_flags:   0;
			accepted_cargos: [];
			animation_info: [ANIMATION_LOOPING, 200];
			animation_speed: 3;
			animation_triggers: bitmask(ANIM_TRIGGER_INDTILE_TILE_LOOP);
		}
	}
	if (param_extension_production_boost) {
		property {
			substitute:         0;
			land_shape_flags:   0;
			accepted_cargos: [[BOOM,8]];
			animation_info: [ANIMATION_LOOPING, 200];
			animation_speed: 3;
			animation_triggers: bitmask(ANIM_TRIGGER_INDTILE_TILE_LOOP);
		}
	}
	graphics {
		tile_check: limestone_mine_switch_tile_location_check;
		anim_next_frame: return ((animation_frame == 18) ? CB_RESULT_STOP_ANIMATION : CB_RESULT_NEXT_FRAME);
		anim_control: 0;
		cargo_amount_accept: limestone_mine_switch_tile_cargo_acceptance;
		limestone_mine_switch_graphics;
	}
}

tilelayout limestone_mine_industry_layout_1_tilelayout {
	0, 1: limestone_mine_tile_animated_headgear;
	0, 2: limestone_mine_tile_2;
    1, 0: limestone_mine_tile_2;
    1, 1: limestone_mine_tile_exit_trestle;
    1, 2: limestone_mine_tile_2;
    2, 0: limestone_mine_tile_2;
    2, 1: limestone_mine_tile_exit_trestle;
    2, 2: limestone_mine_tile_2;
    3, 0: limestone_mine_tile_2;
    3, 1: limestone_mine_tile_2;
    3, 2: limestone_mine_tile_2;
}

tilelayout limestone_mine_industry_layout_2_tilelayout {
	0, 0: limestone_mine_tile_2;
    0, 1: limestone_mine_tile_animated_headgear;
    0, 2: limestone_mine_tile_2;
    0, 3: limestone_mine_tile_animated_headgear;
    0, 4: limestone_mine_tile_2;
    1, 0: limestone_mine_tile_2;
    1, 1: limestone_mine_tile_exit_trestle;
    1, 2: limestone_mine_tile_2;
    1, 3: limestone_mine_tile_exit_trestle;
    1, 4: limestone_mine_tile_2;
    2, 0: limestone_mine_tile_2;
    2, 1: limestone_mine_tile_exit_trestle;
    2, 2: limestone_mine_tile_2;
    2, 3: limestone_mine_tile_exit_trestle;
    2, 4: limestone_mine_tile_2;
}

tilelayout limestone_mine_industry_layout_3_tilelayout {
	0, 0: limestone_mine_tile_animated_headgear;
    0, 1: limestone_mine_tile_2;
    1, 0: limestone_mine_tile_exit_trestle;
    1, 1: limestone_mine_tile_2;
    1, 2: limestone_mine_tile_2;
    2, 0: limestone_mine_tile_exit_trestle;
    2, 1: limestone_mine_tile_animated_headgear;
    2, 2: limestone_mine_tile_2;
    3, 2: limestone_mine_tile_2;
    3, 1: limestone_mine_tile_exit_trestle;
    3, 0: limestone_mine_tile_2;
    4, 0: limestone_mine_tile_2;
    4, 1: limestone_mine_tile_exit_trestle;
    4, 2: limestone_mine_tile_2;
    5, 0: limestone_mine_tile_2;
    5, 1: limestone_mine_tile_2;
}

tilelayout limestone_mine_industry_layout_4_tilelayout {
	0, 0: limestone_mine_tile_animated_headgear;
    0, 1: limestone_mine_tile_2;
    0, 2: limestone_mine_tile_animated_headgear;
    0, 3: limestone_mine_tile_2;
    0, 4: limestone_mine_tile_2;
    1, 0: limestone_mine_tile_exit_trestle;
    1, 1: limestone_mine_tile_2;
    1, 2: limestone_mine_tile_exit_trestle;
    1, 3: limestone_mine_tile_2;
    1, 4: limestone_mine_tile_2;
    2, 0: limestone_mine_tile_exit_trestle;
    2, 1: limestone_mine_tile_2;
    2, 2: limestone_mine_tile_exit_trestle;
    2, 3: limestone_mine_tile_2;
    2, 4: limestone_mine_tile_2;
}

switch(FEAT_INDUSTRIES, SELF, limestone_mine_switch_location_check_industry_distance,
        [count_industries(),
		(is_industry_funded_or_prospected()) || (water_distance > 16) &&
		(param_max_industries_per_type_on_map == 0 || param_max_industries_per_type_on_map > industry_count(INDUSTRY_ID_LIMESTONE_MINE)) &&
		(param_max_industries_on_map == 0 || param_max_industries_on_map > LOAD_TEMP(TEMP_REGISTER_NUMBER_OF_INDUSTRIES)) &&
		// distance to related producers
		(industry_distance(INDUSTRY_ID_EXPLOSIVES_FACTORY) >= param_min_dist_related_industry) &&
		// distance to related consumers
		(industry_distance(INDUSTRY_ID_CEMENT_PLANT) >= param_min_dist_related_industry) &&
		(industry_distance(INDUSTRY_ID_LIME_KILN) >= param_min_dist_related_industry) &&
		(industry_distance(INDUSTRY_ID_PAINT_FACTORY) >= param_min_dist_related_industry) && 
		(industry_distance(INDUSTRY_ID_SODA_PLANT) >= param_min_dist_related_industry) &&
		// distance between instances of same industry
		(industry_distance(INDUSTRY_ID_LIMESTONE_MINE) >= param_min_dist_same_industry)
		]) 
{ 
	1: return CB_RESULT_LOCATION_ALLOW;
	return CB_RESULT_LOCATION_DISALLOW;
}


switch(FEAT_INDUSTRIES, SELF, limestone_mine_func_calc_max_prod, 
	65535) {
	return;
}

// if nothing has been transported at all, do not change production (industry might just not be serviced)
switch(FEAT_INDUSTRIES, SELF, limestone_mine_switch_prod_change, 
	[STORE_TEMP(transported_last_month_pct("LIME"), TEMP_REGISTER_TRANSPORTED_LAST_MONTH_PCT_CARGO0),
	 STORE_TEMP(limestone_mine_func_calc_max_prod(),TEMP_REGISTER_MAXIMUM_PRODUCTION),
	 LOAD_PERM(PERM_REGISTER_RESOURCES) > 0])
{
	1: change_production; // use default rules to adapt production
	return CB_RESULT_IND_PROD_CLOSE; // resources are depleted - close
}

// randomize production at build time based on random bits: 96t min, 152t max
switch(FEAT_INDUSTRIES, SELF, limestone_mine_switch_prod_change_build,
	[initialize_extractive_resources(),
	STORE_TEMP(max(limestone_mine_func_calc_max_prod(),128),TEMP_REGISTER_MAXIMUM_PRODUCTION)	 
	])
{
	randomize_initial_production;
}

produce(limestone_mine_produce,
	[],
	[LIME: LOAD_TEMP(TEMP_REGISTER_PRODUCTION_AMOUNT);],
	0 // do not run again
)

////////////////////////////////////////////////////////////////////////////////
// calculate how much to produce, based on remaining reserves, current production level etc.
////////////////////////////////////////////////////////////////////////////////
switch(FEAT_INDUSTRIES, SELF, limestone_mine_switch_produce,
	[init_primary_production(),
	 update_permanent_storage_production_values(),
	 reduce_primary_resources()
	 ])
{
	limestone_mine_produce;
}

produce(limestone_mine_produce_ext_production_boost,
	[BOOM: LOAD_TEMP(TEMP_REGISTER_PRODUCTION_RAW0);],
	[LIME: LOAD_TEMP(TEMP_REGISTER_PRODUCTION_AMOUNT);],
	0 // do not run again
)

switch(FEAT_INDUSTRIES, SELF, limestone_mine_switch_produce_ext_production_boost,
	[init_primary_production(),
	 STORE_TEMP(incoming_cargo_waiting("BOOM"), TEMP_REGISTER_INCOMING_OPT_CARGO_WAITING0),
	 
	 limit_production_optional_cargo(1),

	 STORE_TEMP(min(LOAD_TEMP(TEMP_REGISTER_INCOMING_OPT_CARGO_WAITING0), LOAD_TEMP(TEMP_REGISTER_PRODUCTION_AMOUNT) / 10), TEMP_REGISTER_PRODUCTION_RAW0),
	 
	 update_permanent_storage_production_values(),
	 reduce_primary_resources()
	]) {
	limestone_mine_produce_ext_production_boost;
}

////////////////////////////////////////////////////////////////////////////////
// build the industry window text here
////////////////////////////////////////////////////////////////////////////////
switch(FEAT_INDUSTRIES, SELF, limestone_mine_switch_extra_text, 
	[STORE_TEMP(limestone_mine_func_calc_max_prod(),TEMP_REGISTER_MAXIMUM_PRODUCTION)])
{
	 create_extractive_industry_text;
}

switch(FEAT_INDUSTRIES, SELF, limestone_mine_switch_extra_text_ext_production_boost, 
	[STORE_TEMP(limestone_mine_func_calc_max_prod(),TEMP_REGISTER_MAXIMUM_PRODUCTION)])
{
	 create_extractive_industry_text_optional_cargo;
}

switch(FEAT_INDUSTRIES, SELF, limestone_mine_switch_extra_text_fund, 
    [STORE_TEMP(string(STR_1800), TEMP_REGISTER_EXTRA_TEXT_ARG0),
	current_year]) {
	1800..500000: return string(STR_EMPTY);
	return string(STR_INDUSTRY_NOT_AVAILABLE_BEFORE);
}

// year < 1800: no creation
switch(FEAT_INDUSTRIES, SELF, limestone_mine_switch_check_availability, current_year) {
	0..1799: return CB_RESULT_IND_NO_CONSTRUCTION;               // no limestone mines before 1800
	get_construction_probability;
}

switch(FEAT_INDUSTRIES, SELF, limestone_mine_switch_random_prod_change, 
	[STORE_TEMP(last_month_transported("LIME"),TEMP_REGISTER_TRANSPORTED_LAST_MONTH_PCT_CARGO0)]) {
	extractive_industry_switch_random_prod_change;
}

switch(FEAT_INDUSTRIES, SELF, limestone_mine_switch_cargo_subtype, getbits(extra_callback_info2, 16, 8)) {
	BOOM: switch_cargo_subtype_white;
	return 0x3800 + string(STR_EMPTY);
}

produce(limestone_mine_produce_cap_stockpile,
	[BOOM: incoming_cargo_waiting("BOOM") > 60000 ? incoming_cargo_waiting("BOOM") - 60000 : 0;
	 ],
	[],
	0 // do not run again
)

switch(FEAT_INDUSTRIES, SELF, limestone_mine_switch_stockpile_check, param_stockpile_cap)
{
	1: empty_produce;
	limestone_mine_produce_cap_stockpile;
}

if (param_extension_building_materials && !param_extension_production_boost) {

item(FEAT_INDUSTRIES, limestone_mine, INDUSTRY_ID_LIMESTONE_MINE) {
	property {
		substitute: 0;
		map_colour: 69;
		life_type: IND_LIFE_TYPE_EXTRACTIVE;
		min_cargo_distr: 1;
		layouts: [limestone_mine_industry_layout_1_tilelayout,limestone_mine_industry_layout_2_tilelayout,limestone_mine_industry_layout_3_tilelayout,limestone_mine_industry_layout_4_tilelayout];
		spec_flags: bitmask(IND_FLAG_LONG_CARGO_TYPE_LISTS);
		conflicting_ind_types: [];
		random_sound_effects: [];
		name: string(STR_IND_LIMESTONE_MINE);
		prob_map_gen: 7;
		prob_in_game: 4;
		prospect_chance: 0.75;
		fund_cost_multiplier: 232;
		remove_cost_multiplier: 0;
		remove_cost_multiplier: 0;
		cargo_types: [produce_cargo("LIME",0)];
		nearby_station_name: string(STR_STATION, string(STR_TOWN),string(STR_STATION_LIMESTONE_MINE));
	}

	graphics {
		construction_probability: limestone_mine_switch_check_availability;
		produce_cargo_arrival:    empty_produce; 
		produce_256_ticks:        limestone_mine_switch_produce;
		monthly_prod_change:      limestone_mine_switch_prod_change;
		random_prod_change:       limestone_mine_switch_random_prod_change;
		build_prod_change:        limestone_mine_switch_prod_change_build;
		location_check:           limestone_mine_switch_location_check_industry_distance;
		extra_text_fund:          limestone_mine_switch_extra_text_fund;
		extra_text_industry:      limestone_mine_switch_extra_text;
	}
}
    
} // if param_extension_building_materials

if (param_extension_building_materials && param_extension_production_boost) {

	item(FEAT_INDUSTRIES, limestone_mine, INDUSTRY_ID_LIMESTONE_MINE) {
		property {
			substitute: 0;
			map_colour: 69;
			life_type: IND_LIFE_TYPE_EXTRACTIVE;
			min_cargo_distr: 1;
			layouts: [limestone_mine_industry_layout_1_tilelayout,limestone_mine_industry_layout_2_tilelayout,limestone_mine_industry_layout_3_tilelayout,limestone_mine_industry_layout_4_tilelayout];
			spec_flags: bitmask(IND_FLAG_LONG_CARGO_TYPE_LISTS);
			conflicting_ind_types: [];
			random_sound_effects: [];
			name: string(STR_IND_LIMESTONE_MINE);
			prob_map_gen: 7;
			prob_in_game: 4;
			prospect_chance: 0.75;
			fund_cost_multiplier: 232;
			remove_cost_multiplier: 0;
			remove_cost_multiplier: 0;
			cargo_types: [accept_cargo("BOOM"),produce_cargo("LIME",0)];
			nearby_station_name: string(STR_STATION, string(STR_TOWN),string(STR_STATION_LIMESTONE_MINE));
		}

		graphics {
			construction_probability: limestone_mine_switch_check_availability;
			produce_cargo_arrival:    limestone_mine_switch_stockpile_check; 
			produce_256_ticks:        limestone_mine_switch_produce_ext_production_boost;
			monthly_prod_change:      limestone_mine_switch_prod_change;
			random_prod_change:       limestone_mine_switch_random_prod_change;
			build_prod_change:        limestone_mine_switch_prod_change_build;
			cargo_subtype_display:    limestone_mine_switch_cargo_subtype;
			location_check:           limestone_mine_switch_location_check_industry_distance;
			extra_text_fund:          limestone_mine_switch_extra_text_fund;
			extra_text_industry:      limestone_mine_switch_extra_text_ext_production_boost;
			stop_accept_cargo:        switch_stop_accept_cargo;
		}
	}
    
}
