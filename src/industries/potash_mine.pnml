spriteset(potash_mine_spriteset_0_0) {
	[10, 310, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
	[80, 310, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
	[150, 310, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
}
spriteset(potash_mine_spriteset_1_0) {
	[10, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
	[80, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
	[150, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
	[220, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
	[290, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
	[360, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
	[430, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
	[500, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
	[500, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
	[570, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
	[570, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
	[640, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
	[640, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
	[710, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
	[780, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
	[850, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
	[920, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
	[990, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
	[1060, 160, 64, 122, -31, -88, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
}
spriteset(potash_mine_spriteset_2_0) {
	[10, 10, 64, 122, -31, -90, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
}

spriteset(potash_mine_spriteset_3_0) {
	[80, 10, 64, 122, -31, -74, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
}

spriteset(potash_mine_spriteset_4_0) {
	[150, 10, 64, 122, -31, -90, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
}

spriteset(potash_mine_spriteset_5_0) {
	[220, 10, 64, 122, -31, -90, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
}

spriteset(potash_mine_spriteset_6_0) {
	[290, 10, 64, 122, -31, -90, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
}

spriteset(potash_mine_spriteset_7_0) {
	[360, 10, 64, 122, -31, -90, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
}

spriteset(potash_mine_spriteset_8_0) {
	[430, 10, 64, 122, -31, -90, ANIM | NOCROP, "graphics/industries/potash_mine.png"]
}

spritelayout potash_mine_spritelayout_tile_empty {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}
}
spritelayout potash_mine_spritelayout_headgear_animated {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}
	building {
		sprite: potash_mine_spriteset_0_0(1*(animation_frame % 3));
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: LOAD_TEMP(TEMP_REGISTER_HIDE_TILE);
	}
}
spritelayout potash_mine_spritelayout_exit_trestle_animated {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}
	building {
		sprite: potash_mine_spriteset_1_0(1* (animation_frame));
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: LOAD_TEMP(TEMP_REGISTER_HIDE_TILE);
	}
}
spritelayout potash_mine_spritelayout_crusher_front_part {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}
	building {
		sprite: potash_mine_spriteset_2_0(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: LOAD_TEMP(TEMP_REGISTER_HIDE_TILE);
	}
}
spritelayout potash_mine_spritelayout_crusher_rear_part {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}
	building {
		sprite: potash_mine_spriteset_3_0(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: LOAD_TEMP(TEMP_REGISTER_HIDE_TILE);
	}
}
spritelayout potash_mine_spritelayout_hut_vents {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}
	building {
		sprite: potash_mine_spriteset_4_0(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: LOAD_TEMP(TEMP_REGISTER_HIDE_TILE);
	}
}
spritelayout potash_mine_spritelayout_ore_1 {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}
	building {
		sprite: potash_mine_spriteset_5_0(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: LOAD_TEMP(TEMP_REGISTER_HIDE_TILE);
	}
}
spritelayout potash_mine_spritelayout_ore_2 {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}
	building {
		sprite: potash_mine_spriteset_6_0(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: LOAD_TEMP(TEMP_REGISTER_HIDE_TILE);
	}
}
spritelayout potash_mine_spritelayout_winding_house {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}
	building {
		sprite: potash_mine_spriteset_7_0(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: LOAD_TEMP(TEMP_REGISTER_HIDE_TILE);
	}
	building {
		sprite: TTD_SMOKE_SPRITE_DARK_SMALL + (animation_frame / 4);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: -1;
		yoffset: 2;
		zoffset: 38+ animation_frame;
		xextent: 11;
		yextent: 16;
		zextent: 7;
		hide_sprite: animation_frame > 19;
	}
	building {
		sprite: TTD_SMOKE_SPRITE_DARK_SMALL + (animation_frame / 4);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: -1;
		yoffset: 6;
		zoffset: 38+ animation_frame;
		xextent: 11;
		yextent: 16;
		zextent: 7;
		hide_sprite: animation_frame > 19;
	}
}
spritelayout potash_mine_spritelayout_exit_shed_rear {
	childsprite {
		sprite: TTD_GROUND_SPRITE_MUD_TRACK;
		hide_sprite: 0;
		always_draw: 1;
	}
	building {
		sprite: potash_mine_spriteset_8_0(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: LOAD_TEMP(TEMP_REGISTER_HIDE_TILE);
	}
}

switch(FEAT_INDUSTRYTILES, SELF, potash_mine_industry_layout_1, relative_pos) {
	relative_coord(0, 1): potash_mine_spritelayout_headgear_animated;
	relative_coord(0, 2): potash_mine_spritelayout_winding_house;
	relative_coord(1, 0): potash_mine_spritelayout_crusher_rear_part;
	relative_coord(1, 1): potash_mine_spritelayout_exit_shed_rear;
	relative_coord(1, 2): potash_mine_spritelayout_hut_vents;
	relative_coord(2, 0): potash_mine_spritelayout_tile_empty;
	relative_coord(2, 1): potash_mine_spritelayout_exit_trestle_animated;
	relative_coord(2, 2): potash_mine_spritelayout_tile_empty;
	relative_coord(3, 0): potash_mine_spritelayout_crusher_front_part;
	relative_coord(3, 1): potash_mine_spritelayout_ore_1;
	relative_coord(3, 2): potash_mine_spritelayout_ore_2;
	return 0;
}
switch(FEAT_INDUSTRYTILES, SELF, potash_mine_industry_layout_2, relative_pos) {
	relative_coord(0, 0): potash_mine_spritelayout_crusher_rear_part;
	relative_coord(0, 1): potash_mine_spritelayout_headgear_animated;
	relative_coord(0, 2): potash_mine_spritelayout_winding_house;
	relative_coord(0, 3): potash_mine_spritelayout_headgear_animated;
	relative_coord(0, 4): potash_mine_spritelayout_winding_house;
	relative_coord(1, 0): potash_mine_spritelayout_tile_empty;
	relative_coord(1, 1): potash_mine_spritelayout_exit_shed_rear;
	relative_coord(1, 2): potash_mine_spritelayout_tile_empty;
	relative_coord(1, 3): potash_mine_spritelayout_exit_shed_rear;
	relative_coord(1, 4): potash_mine_spritelayout_hut_vents;
	relative_coord(2, 0): potash_mine_spritelayout_crusher_front_part;
	relative_coord(2, 1): potash_mine_spritelayout_exit_trestle_animated;
	relative_coord(2, 2): potash_mine_spritelayout_ore_1;
	relative_coord(2, 3): potash_mine_spritelayout_exit_trestle_animated;
	relative_coord(2, 4): potash_mine_spritelayout_ore_2;
	return 0;
}
switch(FEAT_INDUSTRYTILES, SELF, potash_mine_industry_layout_3, relative_pos) {
	relative_coord(0, 0): potash_mine_spritelayout_headgear_animated;
	relative_coord(0, 1): potash_mine_spritelayout_winding_house;
	relative_coord(1, 0): potash_mine_spritelayout_exit_shed_rear;
	relative_coord(1, 1): potash_mine_spritelayout_ore_2;
	relative_coord(1, 2): potash_mine_spritelayout_tile_empty;
	relative_coord(2, 0): potash_mine_spritelayout_exit_trestle_animated;
	relative_coord(2, 1): potash_mine_spritelayout_headgear_animated;
	relative_coord(2, 2): potash_mine_spritelayout_winding_house;
	relative_coord(3, 2): potash_mine_spritelayout_hut_vents;
	relative_coord(3, 1): potash_mine_spritelayout_exit_shed_rear;
	relative_coord(3, 0): potash_mine_spritelayout_crusher_rear_part;
	relative_coord(4, 0): potash_mine_spritelayout_tile_empty;
	relative_coord(4, 1): potash_mine_spritelayout_exit_trestle_animated;
	relative_coord(4, 2): potash_mine_spritelayout_ore_2;
	relative_coord(5, 0): potash_mine_spritelayout_crusher_front_part;
	relative_coord(5, 1): potash_mine_spritelayout_ore_1;
	return 0;
}
switch(FEAT_INDUSTRYTILES, SELF, potash_mine_industry_layout_4, relative_pos) {
	relative_coord(0, 0): potash_mine_spritelayout_headgear_animated;
	relative_coord(0, 1): potash_mine_spritelayout_winding_house;
	relative_coord(0, 2): potash_mine_spritelayout_headgear_animated;
	relative_coord(0, 3): potash_mine_spritelayout_winding_house;
	relative_coord(0, 4): potash_mine_spritelayout_crusher_rear_part;
	relative_coord(1, 0): potash_mine_spritelayout_exit_shed_rear;
	relative_coord(1, 1): potash_mine_spritelayout_ore_2;
	relative_coord(1, 2): potash_mine_spritelayout_exit_shed_rear;
	relative_coord(1, 3): potash_mine_spritelayout_tile_empty;
	relative_coord(1, 4): potash_mine_spritelayout_tile_empty;
	relative_coord(2, 0): potash_mine_spritelayout_exit_trestle_animated;
	relative_coord(2, 1): potash_mine_spritelayout_hut_vents;
	relative_coord(2, 2): potash_mine_spritelayout_exit_trestle_animated;
	relative_coord(2, 3): potash_mine_spritelayout_ore_1;
	relative_coord(2, 4): potash_mine_spritelayout_crusher_front_part;
	return 0;
}         

switch(FEAT_INDUSTRYTILES, PARENT, potash_mine_switch_graphics_layouts, layout_num) {
	1: potash_mine_industry_layout_1;
	2: potash_mine_industry_layout_2;
	3: potash_mine_industry_layout_3;
	4: potash_mine_industry_layout_4;
    potash_mine_industry_layout_1; // default
}

switch (FEAT_INDUSTRYTILES, SELF, potash_mine_switch_graphics,
	[switch_terrain_sprite(),switch_graphics_init_temp_registers()

	]) {
    potash_mine_switch_graphics_layouts;
}

//////////////////////////////////////////////////////////////////////////////
// location checks
//////////////////////////////////////////////////////////////////////////////
switch (FEAT_INDUSTRYTILES, SELF, potash_mine_switch_tile_location_check2,
	(nearby_tile_height(-relative_x, -relative_y) + 
	(nearby_tile_slope(-relative_x, -relative_y)== SLOPE_FLAT ? 0 : nearby_tile_slope(-relative_x, -relative_y) == bitmask(IS_STEEP_SLOPE) ? 2 : 1)) 
	== 
	(nearby_tile_height(0, 0) + (nearby_tile_slope(0, 0) == SLOPE_FLAT ? 0 : nearby_tile_slope(0, 0) == bitmask(IS_STEEP_SLOPE) ? 2 : 1))
	) {
	1: return CB_RESULT_LOCATION_ALLOW;
	return CB_RESULT_LOCATION_DISALLOW;
}


switch (FEAT_INDUSTRYTILES, SELF, potash_mine_switch_tile_location_check,
	is_funded_or_prospected() || (generation_not_forbidden_by_param() && not_next_to_industry()))
{
	1: potash_mine_switch_tile_location_check2;
	return CB_RESULT_LOCATION_DISALLOW;
}

switch(FEAT_INDUSTRYTILES, PARENT, potash_mine_switch_tile_cargo_acceptance, 
	[STORE_TEMP(incoming_cargo_waiting("BOOM") > 60000 ? 0 : 8,TEMP_REGISTER_STOCKPILE_FULL0)
	]
)
{
	return LOAD_TEMP(TEMP_REGISTER_STOCKPILE_FULL0);
}
	
//////////////////////////////////////////////////////////////////////////////
// industry tile and layout definition
//////////////////////////////////////////////////////////////////////////////
item(FEAT_INDUSTRYTILES, potash_mine_tile_1, INDUSTRY_TILE_ID_POTASH_MINE_1) {
	property {
		substitute:         0;
		land_shape_flags:   0;
		accepted_cargos: [[BOOM,8]];
		animation_info: [ANIMATION_LOOPING, 81];
		animation_speed: 1;
		animation_triggers: bitmask(ANIM_TRIGGER_INDTILE_TILE_LOOP);
	}
	graphics {
		tile_check: potash_mine_switch_tile_location_check;
		anim_next_frame: return ((animation_frame == 80) ? CB_RESULT_STOP_ANIMATION : CB_RESULT_NEXT_FRAME);
		anim_control: 0;
		cargo_amount_accept: potash_mine_switch_tile_cargo_acceptance;
		potash_mine_switch_graphics;
	}
}
item(FEAT_INDUSTRYTILES, potash_mine_tile_2, INDUSTRY_TILE_ID_POTASH_MINE_2) {
	property {
		substitute:         0;
		land_shape_flags:   0;
		accepted_cargos: [[BOOM,8]];
		animation_info: [ANIMATION_LOOPING, 71];
		animation_speed: 2;
		animation_triggers: bitmask(ANIM_TRIGGER_INDTILE_CONSTRUCTION_STATE);
	}
	graphics {
		tile_check: potash_mine_switch_tile_location_check;
		anim_control: 0;
		cargo_amount_accept: potash_mine_switch_tile_cargo_acceptance;
		potash_mine_switch_graphics;
	}
}
item(FEAT_INDUSTRYTILES, potash_mine_tile_3, INDUSTRY_TILE_ID_POTASH_MINE_3) {
	property {
		substitute:         0;
		land_shape_flags:   0;
		accepted_cargos: [[BOOM,8]];
		animation_info: [ANIMATION_LOOPING, 200];
		animation_speed: 3;
		animation_triggers: bitmask(ANIM_TRIGGER_INDTILE_TILE_LOOP);
	}
	graphics {
		tile_check: potash_mine_switch_tile_location_check;
		anim_next_frame: return ((animation_frame == 18) ? CB_RESULT_STOP_ANIMATION : CB_RESULT_NEXT_FRAME);
        anim_control: 0;
		cargo_amount_accept: potash_mine_switch_tile_cargo_acceptance;
		potash_mine_switch_graphics;
	}
}

tilelayout potash_mine_industry_layout_1_tilelayout {
	0, 1: potash_mine_tile_1;
	0, 2: potash_mine_tile_2;
	1, 0: potash_mine_tile_2;
	1, 1: potash_mine_tile_2;
	1, 2: potash_mine_tile_2;
	2, 0: potash_mine_tile_2;
	2, 1: potash_mine_tile_3;
	2, 2: potash_mine_tile_2;
	3, 0: potash_mine_tile_2;
	3, 1: potash_mine_tile_2;
	3, 2: potash_mine_tile_2;
}
tilelayout potash_mine_industry_layout_2_tilelayout {
	0, 0: potash_mine_tile_2;
	0, 1: potash_mine_tile_1;
	0, 2: potash_mine_tile_2;
	0, 3: potash_mine_tile_1;
	0, 4: potash_mine_tile_2;
	1, 0: potash_mine_tile_2;
	1, 1: potash_mine_tile_2;
	1, 2: potash_mine_tile_2;
	1, 3: potash_mine_tile_2;
	1, 4: potash_mine_tile_2;
	2, 0: potash_mine_tile_2;
	2, 1: potash_mine_tile_3;
	2, 2: potash_mine_tile_2;
	2, 3: potash_mine_tile_3;
	2, 4: potash_mine_tile_2;
}
tilelayout potash_mine_industry_layout_3_tilelayout {
	0, 0: potash_mine_tile_1;
	0, 1: potash_mine_tile_2;
	1, 0: potash_mine_tile_2;
	1, 1: potash_mine_tile_2;
	1, 2: potash_mine_tile_2;
	2, 0: potash_mine_tile_3;
	2, 1: potash_mine_tile_1;
	2, 2: potash_mine_tile_2;
	3, 2: potash_mine_tile_2;
	3, 1: potash_mine_tile_2;
	3, 0: potash_mine_tile_2;
	4, 0: potash_mine_tile_2;
	4, 1: potash_mine_tile_3;
	4, 2: potash_mine_tile_2;
	5, 0: potash_mine_tile_2;
	5, 1: potash_mine_tile_2;
}
tilelayout potash_mine_industry_layout_4_tilelayout {
	0, 0: potash_mine_tile_1;
	0, 1: potash_mine_tile_2;
	0, 2: potash_mine_tile_1;
	0, 3: potash_mine_tile_2;
	0, 4: potash_mine_tile_2;
	1, 0: potash_mine_tile_2;
	1, 1: potash_mine_tile_2;
	1, 2: potash_mine_tile_2;
	1, 3: potash_mine_tile_2;
	1, 4: potash_mine_tile_2;
	2, 0: potash_mine_tile_3;
	2, 1: potash_mine_tile_2;
	2, 2: potash_mine_tile_3;
	2, 3: potash_mine_tile_2;
	2, 4: potash_mine_tile_2;
}

switch(FEAT_INDUSTRIES, SELF, potash_mine_switch_location_check_industry_distance,
        [count_industries(),
		(is_industry_funded_or_prospected()) || ((water_distance > 16) &&
		(param_max_industries_per_type_on_map == 0 || param_max_industries_per_type_on_map > industry_count(INDUSTRY_ID_COPPER_ORE_MINE)) &&
		(param_max_industries_on_map == 0 || param_max_industries_on_map > LOAD_TEMP(TEMP_REGISTER_NUMBER_OF_INDUSTRIES)) &&
		// distance to related producers
		(industry_distance(INDUSTRY_ID_EXPLOSIVES_FACTORY) >= param_min_dist_related_industry) && 
		// distance to related consumers
		(industry_distance(INDUSTRY_ID_FERTILIZER_PLANT) >= param_min_dist_related_industry) &&
		(industry_distance(INDUSTRY_ID_CLEANING_PRODUCTS_FACTORY) >= param_min_dist_related_industry) &&
		(industry_distance(INDUSTRY_ID_GLASS_WORKS) >= param_min_dist_related_industry) &&
		// distance between instances of same industry
		(industry_distance(INDUSTRY_ID_POTASH_MINE) >= param_min_dist_same_industry)
		)]) 
{ 
	1: return CB_RESULT_LOCATION_ALLOW;
	return CB_RESULT_LOCATION_DISALLOW;
}


switch(FEAT_INDUSTRIES, SELF, potash_mine_func_calc_max_prod, 
	param_economy_model == 0 ? 65535 :
	  current_year <= 1859 ? 4 : 
	  current_year <= 1894 ? (current_year-1860)+4 : 
	  current_year <= 1914 ? (current_year-1895)*4+40 : 
	  current_year <= 1923 ? (current_year-1915)+120 : 
	  65535) {
	return;
}

// if nothing has been transported at all, do not change production (industry might just not be serviced)
switch(FEAT_INDUSTRIES, SELF, potash_mine_switch_prod_change, 
	[STORE_TEMP(transported_last_month_pct("POTA"), TEMP_REGISTER_TRANSPORTED_LAST_MONTH_PCT_CARGO0),
	 STORE_TEMP(potash_mine_func_calc_max_prod(),TEMP_REGISTER_MAXIMUM_PRODUCTION),
	 
	 LOAD_PERM(PERM_REGISTER_RESOURCES) > 0])
{
	1: change_production; // use default rules to adapt production
	return CB_RESULT_IND_PROD_CLOSE; // resources are depleted - close
}

// randomize production at build time based on random bits: 96t min, 152t max
switch(FEAT_INDUSTRIES, SELF, potash_mine_switch_prod_change_build,
	[initialize_extractive_resources(),
	STORE_TEMP(potash_mine_func_calc_max_prod(),TEMP_REGISTER_MAXIMUM_PRODUCTION)	 
	])
{
	randomize_initial_production;
}

produce(potash_mine_produce,
	[BOOM: LOAD_TEMP(TEMP_REGISTER_PRODUCTION_RAW0);],
	[POTA: LOAD_TEMP(TEMP_REGISTER_PRODUCTION_AMOUNT);],
	0 // do not run again
)

////////////////////////////////////////////////////////////////////////////////
// calculate how much to produce, based on remaining reserves, current production level etc.
////////////////////////////////////////////////////////////////////////////////
switch(FEAT_INDUSTRIES, SELF, potash_mine_switch_produce,
	[init_primary_production(),
	 STORE_TEMP(incoming_cargo_waiting("BOOM"), TEMP_REGISTER_INCOMING_OPT_CARGO_WAITING0),
	 
	 limit_production_optional_cargo(1),
	 
	 STORE_TEMP(min(LOAD_TEMP(TEMP_REGISTER_INCOMING_OPT_CARGO_WAITING0), LOAD_TEMP(TEMP_REGISTER_PRODUCTION_AMOUNT) / 10), TEMP_REGISTER_PRODUCTION_RAW0),
	 
	 update_permanent_storage_production_values(),
	 reduce_primary_resources()
	]) {
	potash_mine_produce;
}

////////////////////////////////////////////////////////////////////////////////
// build the industry window text here
////////////////////////////////////////////////////////////////////////////////
switch(FEAT_INDUSTRIES, SELF, potash_mine_switch_extra_text_ext_production_boost, 
	[STORE_TEMP(potash_mine_func_calc_max_prod(),TEMP_REGISTER_MAXIMUM_PRODUCTION)])
{
	 create_extractive_industry_text_optional_cargo;
}

switch(FEAT_INDUSTRIES, SELF, potash_mine_switch_extra_text_fund, 
    [STORE_TEMP(string(STR_1800), TEMP_REGISTER_EXTRA_TEXT_ARG0),
	current_year]) {
	1800..500000: return string(STR_EMPTY);
	return string(STR_INDUSTRY_NOT_AVAILABLE_BEFORE);
}

switch(FEAT_INDUSTRIES, SELF, potash_mine_switch_random_prod_change, 
	[STORE_TEMP(last_month_transported("POTA"),TEMP_REGISTER_TRANSPORTED_LAST_MONTH_PCT_CARGO0)]) {
	extractive_industry_switch_random_prod_change;
}

// year < 1800: no creation, 1800 <= year: certain probability
switch(FEAT_INDUSTRIES, SELF, potash_mine_switch_check_availability, current_year) {
	0..1799: return CB_RESULT_IND_NO_CONSTRUCTION;               // no salt mines before 1800
	get_construction_probability;
}

switch(FEAT_INDUSTRIES, SELF, potash_mine_switch_cargo_subtype, getbits(extra_callback_info2, 16, 8)) {
	BOOM: switch_cargo_subtype_white;
	return 0x3800 + string(STR_EMPTY);
}

produce(potash_mine_produce_cap_stockpile,
	[BOOM: incoming_cargo_waiting("BOOM") > 60000 ? incoming_cargo_waiting("BOOM") - 60000 : 0;
	 ],
	[],
	0 // do not run again
)

switch(FEAT_INDUSTRIES, SELF, potash_mine_switch_stockpile_check, param_stockpile_cap)
{
	1: empty_produce;
	potash_mine_produce_cap_stockpile;
}

if (param_extension_production_boost) {
	item(FEAT_INDUSTRIES, potash_mine, INDUSTRY_ID_POTASH_MINE) {
		property {
			substitute: 0;
			map_colour: 127;
			life_type: IND_LIFE_TYPE_EXTRACTIVE;
			min_cargo_distr: 1;
			layouts: [potash_mine_industry_layout_1_tilelayout,potash_mine_industry_layout_2_tilelayout,potash_mine_industry_layout_3_tilelayout,potash_mine_industry_layout_4_tilelayout];
			spec_flags: bitmask(IND_FLAG_LONG_CARGO_TYPE_LISTS);
			conflicting_ind_types: [];
			random_sound_effects: [];
			name: string(STR_IND_POTASH_MINE);
			prob_map_gen: 7;
            prob_in_game: 4;
            prospect_chance: 0.75;
            fund_cost_multiplier: 232;
			remove_cost_multiplier: 0;
			remove_cost_multiplier: 0;
			cargo_types: [accept_cargo("BOOM"),produce_cargo("POTA",0)];
			nearby_station_name: string(STR_STATION, string(STR_TOWN),string(STR_STATION_POTASH_MINE));
		}

		graphics {
			construction_probability: potash_mine_switch_check_availability;
			produce_cargo_arrival:    potash_mine_switch_stockpile_check; 
			produce_256_ticks:        potash_mine_switch_produce;
			monthly_prod_change:      potash_mine_switch_prod_change;
			random_prod_change:       potash_mine_switch_random_prod_change;
			build_prod_change:        potash_mine_switch_prod_change_build;
			cargo_subtype_display:    potash_mine_switch_cargo_subtype;
			location_check:           potash_mine_switch_location_check_industry_distance;
			extra_text_fund:          potash_mine_switch_extra_text_fund;
			extra_text_industry:      potash_mine_switch_extra_text_ext_production_boost;
			stop_accept_cargo:        switch_stop_accept_cargo;
		}
	}    
}

