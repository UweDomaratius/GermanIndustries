spriteset(recycling_depot_spriteset_2_0) {
	[10, 10, 64, 31, -31, 0, ANIM | NOCROP, "graphics/industries/recycling_depot_1.png"]
}
spriteset(recycling_depot_spriteset_2_0_snow) {
	[10, 10, 64, 31, -31, 0, ANIM | NOCROP, "graphics/industries/recycling_depot_1_snow.png"]
}
spriteset(recycling_depot_spriteset_3_0) {
	[80, 10, 64, 31, -31, 0, ANIM | NOCROP, "graphics/industries/recycling_depot_1.png"]
}
spriteset(recycling_depot_spriteset_3_0_snow) {
	[80, 10, 64, 31, -31, 0, ANIM | NOCROP, "graphics/industries/recycling_depot_1_snow.png"]
}

spritelayout recycling_depot_spritelayout_hut {
	childsprite {
		sprite: default_spriteset_ground_dirty_concrete;
		hide_sprite: 0;
		always_draw: 1;
	}
	building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_NE) == 0);
		xoffset:  0;
		yoffset:  0;
		zoffset:  0;
		xextent: 1;
		yextent: 16;
		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_NW) == 0);
		xoffset:  0;
		yoffset:  0;
		zoffset:  0;
		xextent: 16;
		yextent: 1;
		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: recycling_depot_spriteset_2_0(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE));
	}
	building {
		sprite: recycling_depot_spriteset_2_0_snow(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE_SNOW));
	}
	building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SE) == 0);
		xoffset: 0;
		yoffset:  16;
		zoffset:  0;
		xextent: 16;
		yextent: 1;
		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SW) == 0);
		xoffset: 16;
		yoffset:  0;
		zoffset:  0;
		xextent: 1;
		yextent: 16;
		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
}
spritelayout recycling_depot_spritelayout_no_hut {
	childsprite {
		sprite: default_spriteset_ground_dirty_concrete;
		hide_sprite: 0;
		always_draw: 1;
	}
    building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_NE) == 0);
		xoffset:  0;
		yoffset:  0;
		zoffset:  0;
		xextent: 1;
		yextent: 16;
		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_NW) == 0);
		xoffset:  0;
		yoffset:  0;
		zoffset:  0;
		xextent: 16;
		yextent: 1;
		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: recycling_depot_spriteset_3_0(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE));
	}
	building {
		sprite: recycling_depot_spriteset_3_0_snow(0);
		recolour_mode: RECOLOUR_REMAP;
		palette: PALETTE_USE_DEFAULT;
		xoffset: 0;
		yoffset: 0;
		zoffset: 0;
		xextent: 16;
		yextent: 16;
		zextent: 32;
		always_draw: 0;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_HIDE_TILE_SNOW));
	}
	building {
		sprite: TTD_FENCE_X;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SE) == 0);
		xoffset: 0;
		yoffset:  16;
		zoffset:  0;
		xextent: 16;
		yextent: 1;
		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
	building {
		sprite: TTD_FENCE_Y;
		hide_sprite: (LOAD_TEMP(TEMP_REGISTER_FENCE_SW) == 0);
		xoffset: 16;
		yoffset:  0;
		zoffset:  0;
		xextent: 1;
		yextent: 16;
		zextent: 6;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
}

//////////////////////////////////////////////////////////////////////////////
// graphics choice switch chain
//////////////////////////////////////////////////////////////////////////////

switch(FEAT_INDUSTRYTILES, SELF, recycling_depot_switch_graphics_layout, relative_pos) {
	relative_coord(0, 0): recycling_depot_spritelayout_hut;
	relative_coord(0, 1): recycling_depot_spritelayout_no_hut;
	relative_coord(1, 0): recycling_depot_spritelayout_no_hut;
	relative_coord(1, 1): recycling_depot_spritelayout_no_hut;
	return 0;
}

switch(FEAT_INDUSTRYTILES, SELF, recycling_depot_switch_graphics_construction_state, construction_state) {
    0..2: spritelayout_default_construction_states;
    recycling_depot_switch_graphics_layout; // default
}

switch (FEAT_INDUSTRYTILES, SELF, recycling_depot_switch_graphics,
	[switch_tile_fences(),switch_terrain_sprite(),switch_graphics_init_temp_registers()]) {
    recycling_depot_switch_graphics_construction_state;
}

//////////////////////////////////////////////////////////////////////////////
// location checks
//////////////////////////////////////////////////////////////////////////////
// This check works as follows: calculate height of the northernmost tile of the industry and add 0, 1 or 2 for flat, slope. steep slope
// Do the same for the current tile and compare the results. If the result is equal, the terrain is effectively flat
// and industry can be placed.
// It just looks weird due to the stacked ternary operators needed here
switch (FEAT_INDUSTRYTILES, SELF, recycling_depot_switch_tile_location_check2,
			(nearby_tile_height(-relative_x, -relative_y) + 
			(nearby_tile_slope(-relative_x, -relative_y)== SLOPE_FLAT ? 0 : nearby_tile_slope(-relative_x, -relative_y) == bitmask(IS_STEEP_SLOPE) ? 2 : 1)) 
			== 
			(nearby_tile_height(0, 0) + (nearby_tile_slope(0, 0) == SLOPE_FLAT ? 0 : nearby_tile_slope(0, 0) == bitmask(IS_STEEP_SLOPE) ? 2 : 1))
			) {
	1: return CB_RESULT_LOCATION_ALLOW;
	return CB_RESULT_LOCATION_DISALLOW;
}

switch (FEAT_INDUSTRYTILES, SELF, recycling_depot_switch_tile_location_check,
	is_funded_or_prospected() || (generation_not_forbidden_by_param() && not_next_to_industry() && not_next_to_buildings()))
{
	1: recycling_depot_switch_tile_location_check2;
	return CB_RESULT_LOCATION_DISALLOW;
}

//////////////////////////////////////////////////////////////////////////////
// industry tile and layout definition
//////////////////////////////////////////////////////////////////////////////
item(FEAT_INDUSTRYTILES, recycling_depot_tile, INDUSTRY_TILE_ID_RECYCLING_DEPOT) {
	property {
		substitute:         0;
        land_shape_flags:   0;
        special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
        animation_triggers: bitmask();
	}
	graphics {
		tile_check: recycling_depot_switch_tile_location_check;
		recycling_depot_switch_graphics;
	}
}

tilelayout recycling_depot_tilelayout {
	0, 0: recycling_depot_tile;
    0, 1: recycling_depot_tile;
    1, 0: recycling_depot_tile;
    1, 1: recycling_depot_tile;

}

switch(FEAT_INDUSTRIES, SELF, recycling_depot_switch_location_check_industry_distance,
       [STORE_TEMP(0, TEMP_REGISTER_TOWN_MIN_DISTANCE),
	    STORE_TEMP(24, TEMP_REGISTER_TOWN_MAX_DISTANCE),
	    (is_industry_funded_or_prospected()) || (distance_from_town() && 
	    (industry_distance(INDUSTRY_ID_GLASS_WORKS) >= 32) &&
		(industry_distance(INDUSTRY_ID_PAPER_MILL) >= 32) &&
		(industry_distance(INDUSTRY_ID_SCRAP_YARD) >= 32) && 
	    (industry_distance(INDUSTRY_ID_RECYCLING_DEPOT) >= 16))]) {
	   1: return CB_RESULT_LOCATION_ALLOW;
	   return CB_RESULT_LOCATION_DISALLOW;
}

produce(recycling_depot_produce,
	[],
	[RCYC: LOAD_TEMP(TEMP_REGISTER_CURRENT_PRODUCTION_AMOUNT);],
	0 // do not run again
)

// production scales with population of related city, min 4, max 255 per 256 ticks
switch(FEAT_INDUSTRIES, PARENT, recycling_depot_switch_produce, 
	[ STORE_TEMP(min(max(4,population/42),255), TEMP_REGISTER_CURRENT_PRODUCTION_AMOUNT)	
	]) {
	recycling_depot_produce;
}

if (param_extension_recycling && (param_extension_glass || param_extension_paper)) {
	item(FEAT_INDUSTRIES, recycling_depot, INDUSTRY_ID_RECYCLING_DEPOT) {
		property {
			substitute: 0;
			map_colour: 132;
			life_type: IND_LIFE_TYPE_EXTRACTIVE;
			min_cargo_distr: 1;
			layouts: [recycling_depot_tilelayout];
			spec_flags: bitmask(IND_FLAG_LONG_CARGO_TYPE_LISTS);
			conflicting_ind_types: [];
			random_sound_effects: [];
			name: string(STR_IND_RECYCLING_DEPOT);
			prob_map_gen: 20;
			prob_in_game: 20;
			fund_cost_multiplier: 118;
			remove_cost_multiplier: 0;
			remove_cost_multiplier: 0;
			cargo_types: [produce_cargo("RCYC",0)];
			nearby_station_name: string(STR_STATION, string(STR_TOWN),string(STR_STATION_RECYCLING_DEPOT));
		}
			
		graphics {
		    construction_probability: get_construction_probability;
			produce_cargo_arrival:    empty_produce; 
			produce_256_ticks:        recycling_depot_switch_produce;
			random_prod_change:       return CB_RESULT_IND_PROD_NO_CHANGE;
			location_check:           recycling_depot_switch_location_check_industry_distance;
		}
	}
}
